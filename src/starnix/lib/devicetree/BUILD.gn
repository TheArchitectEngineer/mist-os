# Copyright 2025 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/devicetree/devicetree.gni")
import("//build/rust/rustc_library.gni")

group("tests") {
  testonly = true
  deps = [ ":devicetree-tests" ]
}

rustc_library("devicetree") {
  name = "devicetree"
  edition = "2021"
  version = "0.1.0"
  source_root = "src/lib.rs"
  with_unit_tests = true

  sources = [
    "src/lib.rs",
    "src/parser.rs",
    "src/types.rs",
  ]

  deps = [ "//third_party/rust_crates:zerocopy" ]
}

dtb("dtb") {
  sources = [ "//src/devices/board/drivers/vim3-devicetree/dts/vim3.dts" ]
  deps = [ "//src/devices/board/drivers/vim3-devicetree:vim3-power-config" ]
  dtcflags = [ "-Wno-unique_unit_address" ]
}

resource("dtb-file") {
  sources = get_target_outputs(":dtb")
  outputs = [ "test-data/test.dtb" ]
  deps = [ ":dtb" ]
}

fuchsia_unittest_package("devicetree-tests") {
  manifest = "meta/devicetree_tests.cml"
  deps = [
    ":devicetree_test",
    ":dtb-file",
  ]
}
