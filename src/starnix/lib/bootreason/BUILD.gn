# Copyright 2025 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/fidl/fidl.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_library.gni")
import("//src/starnix/build/starnix_linux_executable.gni")
import("//src/starnix/kernel/starnix.gni")

rustc_library("bootreason") {
  edition = "2021"
  sources = [ "src/lib.rs" ]
  deps = [
    "//sdk/fidl/fuchsia.feedback:fuchsia.feedback_rust",
    "//sdk/rust/zx",
    "//src/lib/fuchsia-component",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:log",
  ]

  configs += [ "//src/starnix/config:starnix_clippy_lints" ]
}

group("tests") {
  testonly = true

  deps = [ ":starnix_bootreason_integration_test" ]
}

rustc_test("bootreason_test") {
  testonly = true
  edition = "2021"
  source_root = "tests/lib.rs"
  sources = [ "tests/lib.rs" ]
  deps = [
    "//sdk/fidl/fuchsia.feedback:fuchsia.feedback_rust",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-component-test",
    "//src/lib/fuchsia-fs",
    "//src/sys/lib/component-events",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
  ]
}

fuchsia_test_component("bootreason_integration_test") {
  manifest = "meta/integration_test.cml"
  deps = [
    ":bootreason_test",
    "//src/starnix/config:starnix.profiles",
  ]
  test_type = "starnix"
}

starnix_linux_executable("mount_pstore_bin") {
  sources = [ "tests/mount_pstore.cc" ]
}

fuchsia_test_component("mount_pstore") {
  manifest = "meta/mount_pstore.cml"
  deps = [ ":mount_pstore_bin($target_linux_toolchain)" ]
}

fuchsia_component("container") {
  testonly = true
  manifest = "meta/container.cml"
}

fuchsia_component("test_realm") {
  testonly = true
  manifest = "meta/test_realm.cml"
}

fuchsia_test_package("starnix_bootreason_integration_test") {
  test_components = [ ":bootreason_integration_test" ]
  deps = [
    ":container",
    ":mount_pstore",
    ":test_realm",
    "//src/starnix/containers/debian:container_resources",
  ]
  subpackages = [
    "//src/starnix/kernel:starnix_kernel_package",
    "//src/starnix/containers/debian:debian_package",
  ]
}
