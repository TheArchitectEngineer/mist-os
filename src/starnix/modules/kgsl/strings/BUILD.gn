# Copyright 2025 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_library.gni")

group("strings") {
  public_deps = [ ":kgsl_strings" ]
}

rustc_library("kgsl_strings") {
  visibility = [ ":*" ]
  edition = "2021"
  version = "0.1.0"
  source_root = "$target_gen_dir/lib.rs"

  sources = [
    "$target_gen_dir/kgsl_strings.rs",
    "$target_gen_dir/lib.rs",
  ]

  deps = [
    ":copy_lib",
    ":gen_strings",
    "//src/starnix/lib/starnix_uapi",
  ]
}

copy("copy_lib") {
  visibility = [ ":*" ]
  sources = [ "lib.rs" ]
  outputs = [ "$target_gen_dir/{{source_file_part}}" ]
}

action("gen_strings") {
  visibility = [ ":*" ]
  script = "kgsl_strings.rs.py"
  args = [
    rebase_path("//src/starnix/lib/linux_uapi/src/arm64.rs", root_build_dir),
    rebase_path("$target_gen_dir/kgsl_strings.rs", root_build_dir),
  ]
  inputs = [ "//src/starnix/lib/linux_uapi/src/arm64.rs" ]
  outputs = [ "$target_gen_dir/kgsl_strings.rs" ]
}
