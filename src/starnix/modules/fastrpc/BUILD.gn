# Copyright 2025 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_library.gni")

group("tests") {
  testonly = true
  deps = [ ":starnix-fastrpc-tests" ]
}

fuchsia_package_with_single_component("google_fastrpc") {
  manifest = "meta/fastrpc.cml"
}

rustc_library("fastrpc") {
  name = "starnix-modules-fastrpc"
  edition = "2024"
  source_root = "lib.rs"
  with_unit_tests = true

  sources = [
    "dma_heap.rs",
    "fastrpc.rs",
    "lib.rs",
  ]

  # TODO(https://fxbug.dev/42059075): Re-enable starnix in these environments.
  exclude_toolchain_tags = [
    "asan",
    "hwasan",
  ]

  configs += [ "//src/starnix/config:starnix_clippy_lints" ]

  deps = [
    "//sdk/fidl/fuchsia.hardware.qualcomm.fastrpc:fuchsia.hardware.qualcomm.fastrpc_rust",
    "//sdk/rust/zx",
    "//sdk/rust/zx-status",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-component",
    "//src/starnix/kernel:starnix_core",
    "//src/starnix/kernel:starnix_logging",
    "//src/starnix/lib/linux_uapi",
    "//src/starnix/lib/starnix_sync",
    "//src/starnix/lib/starnix_syscalls",
    "//src/starnix/lib/starnix_types",
    "//src/starnix/lib/starnix_uapi",
    "//third_party/rust_crates:bitfield",
    "//third_party/rust_crates:bstr",
  ]

  test_deps = [ "//src/lib/fuchsia" ]
}

fuchsia_unittest_package("starnix-fastrpc-tests") {
  manifest = "meta/starnix_fastrpc_tests.cml"
  deps = [ ":fastrpc_test" ]
  test_type = "starnix"
}
