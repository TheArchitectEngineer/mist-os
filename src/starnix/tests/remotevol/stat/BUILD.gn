# Copyright 2025 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_test.gni")
import("//src/starnix/build/starnix_linux_rustc_binary.gni")

group("tests") {
  testonly = true
  deps = [ ":starnix_remotevol_stat_locked_file_integration_test" ]
}

starnix_linux_rustc_binary("create_encrypted_file_bin") {
  testonly = true
  edition = 2021
  source_root = "src/debian_programs/create_encrypted_file.rs"
  sources = [ source_root ]
  deps = [
    "//src/starnix/tests/remotevol:remotevol_linux_test_util",
    "//third_party/rust_crates:zerocopy",
  ]
}

fuchsia_component("create_encrypted_file") {
  testonly = true
  manifest = "meta/create_encrypted_file.cml"
  deps = [ ":create_encrypted_file_bin" ]
}

starnix_linux_rustc_binary("stat_encrypted_file_bin") {
  testonly = true
  edition = 2021
  source_root = "src/debian_programs/stat_encrypted_file.rs"
  sources = [ source_root ]
  deps = [ "//src/starnix/tests/remotevol:remotevol_linux_test_util" ]
}

fuchsia_component("stat_encrypted_file") {
  testonly = true
  manifest = "meta/stat_encrypted_file.cml"
  deps = [ ":stat_encrypted_file_bin" ]
}

rustc_test("remotevol_stat_locked_file_integration_test_bin") {
  edition = "2021"
  source_root = "src/stat_locked_file_test.rs"
  sources = [ source_root ]
  deps = [
    "//sdk/fidl/fuchsia.component:fuchsia.component_rust",
    "//sdk/fidl/fuchsia.component.decl:fuchsia.component.decl_rust",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component-test",
    "//src/starnix/tests/remotevol:remotevol_fuchsia_test_util",
    "//src/sys/lib/component-events",
    "//third_party/rust_crates:log",
  ]
}

fuchsia_test_component("remotevol_stat_locked_file_test") {
  manifest = "meta/stat_locked_file_integration_test.cml"

  # fuchsia-component-test has an expect_includes on the realm_builder.shard.cml
  # but this test requires a persistent storage realm builder shard instead.
  check_includes = false
  deps = [ ":remotevol_stat_locked_file_integration_test_bin" ]
  test_type = "starnix"
}

fuchsia_component("kernel_with_container") {
  testonly = true
  manifest = "meta/kernel_with_container.cml"
}

fuchsia_test_package("starnix_remotevol_stat_locked_file_integration_test") {
  test_components = [ ":remotevol_stat_locked_file_test" ]
  deps = [
    ":create_encrypted_file",
    ":kernel_with_container",
    ":stat_encrypted_file",
  ]
  subpackages = [
    "//src/starnix/kernel:starnix_kernel_package",
    "//src/storage/fxfs/test-fxfs:test-fxfs-package",
    "//src/starnix/containers/debian:debian_package",
  ]
}
