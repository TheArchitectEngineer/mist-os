# Copyright 2025 The Fuchsia Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//build/testing/perf/test.gni")

group("benchmarks") {
  testonly = true
  deps = [ ":ebpf_benchmark" ]
}

rustc_binary("bin") {
  output_name = "ebpf_bench"
  edition = "2024"
  testonly = true

  source_root = "src/main.rs"
  sources = [ "src/main.rs" ]

  deps = [
    "//src/developer/fuchsia-criterion",
    "//src/starnix/lib/ebpf",
    "//src/starnix/lib/ebpf_api",
    "//src/starnix/lib/ebpf_loader",
    "//third_party/rust_crates:criterion",
    "//third_party/rust_crates:structopt",
  ]

  configs += [ "//src/starnix/config:starnix_clippy_lints" ]
}

resource("benchmark_prog") {
  visibility = [ ":*" ]
  sources = [ "prog/ebpf_bench.o" ]
  outputs = [ "data/ebpf_bench.o" ]
}

fuchsia_component("ebpf_bench_component") {
  testonly = true
  component_name = "ebpf_bench"
  manifest = "meta/ebpf_bench.cml"
  deps = [
    ":benchmark_prog",
    ":bin",
  ]
}

fuchsia_package("ebpf_bench") {
  testonly = true
  package_name = "ebpf_bench"
  deps = [ ":ebpf_bench_component" ]
}

fuchsia_component_perf_test("ebpf_benchmark") {
  package = ":ebpf_bench"
  component_name = "ebpf_bench"
  expected_metric_names_filepath =
      "//src/tests/end_to_end/perf/expected_metric_names/fuchsia.ebpf.txt"
}
