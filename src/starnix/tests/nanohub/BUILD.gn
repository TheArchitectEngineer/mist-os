# Copyright 2025 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//src/starnix/build/starnix_linux_executable.gni")
import("//src/starnix/kernel/starnix.gni")

group("tests") {
  testonly = true
  deps = [ ":starnix_nanohub_module_test" ]
}

rustc_binary("bin") {
  testonly = true
  edition = "2021"
  output_name = "starnix_nanohub_module_test"
  sources = [
    "src/fake_socket_tunnel.rs",
    "src/main.rs",
  ]
  deps = [
    "//sdk/fidl/fuchsia.hardware.sockettunnel:fuchsia.hardware.sockettunnel_rust",
    "//sdk/rust/zx",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-component-test",
    "//src/sys/lib/component-events",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
  ]
}

fuchsia_test_component("integration_test") {
  manifest = "meta/integration_test.cml"
  deps = [ ":bin" ]
  test_type = "starnix"
}

starnix_linux_executable("generate_linux_nanohub_module") {
  testonly = true
  sources = [ "src/sysfs_reader.cc" ]
}

fuchsia_component("container_with_sysfs_reader") {
  testonly = true
  manifest = "meta/container_with_sysfs_reader.cml"
}

fuchsia_component("nanohub_user") {
  testonly = true
  manifest = "meta/nanohub_user.cml"
  deps = [ ":generate_linux_nanohub_module" ]
}

fuchsia_component("nanohub_container") {
  manifest = "meta/nanohub_container.cml"
}

fuchsia_package("nanohub_container_pkg") {
  deps = [
    ":nanohub_container",
    "//src/starnix/containers/debian:container_resources",
  ]
}

fuchsia_test_package("starnix_nanohub_module_test") {
  test_components = [ ":integration_test" ]
  deps = [
    ":container_with_sysfs_reader",
    ":nanohub_container",
    ":nanohub_user",
    "//src/starnix/containers/debian:container_resources",
  ]
  subpackages = [ "//src/starnix/kernel:starnix_kernel_package" ]
}
