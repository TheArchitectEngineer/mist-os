# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
import("//build/bazel/export_fuchsia_package_to_bazel.gni")
import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_library.gni")

rustc_library("server") {
  name = "server"
  edition = "2021"
  with_unit_tests = true
  deps = [
    "//sdk/fidl/fuchsia.starnix.container:fuchsia.starnix.container_rust",
    "//sdk/fidl/fuchsia.vsock:fuchsia.vsock_rust",
    "//sdk/rust/zx",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-component",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
  ]
  source_root = "src/server.rs"
  sources = [ "src/server.rs" ]
}

rustc_binary("bin") {
  output_name = "adb_proxy"
  edition = "2018"
  deps = [
    ":server",
    "//src/lib/fuchsia",
    "//third_party/rust_crates:anyhow",
  ]
  sources = [ "src/main.rs" ]
}

fuchsia_component("adb_proxy_component") {
  component_name = "adb_proxy"
  manifest = "meta/adb_proxy.cml"
  deps = [ ":bin" ]
}

fuchsia_package("adb_proxy") {
  package_name = "adb_proxy"
  deps = [ ":adb_proxy_component" ]
}

# Make `adb_proxy` available to products assembled with Bazel.
export_fuchsia_package_to_bazel("adb_proxy_bazel_inputs") {
  package = ":adb_proxy"
  gn_targets_name = "adb_proxy_archive"
}
