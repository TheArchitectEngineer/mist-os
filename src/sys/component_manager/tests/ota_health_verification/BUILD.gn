# Copyright 2025 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/component/config.gni")
import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_test.gni")

group("tests") {
  testonly = true

  deps = [ ":ota-health-verification-test" ]
}

rustc_test("ota_health_verification_test_bin") {
  name = "ota_health_verification_test"
  edition = "2021"
  source_root = "integration_test.rs"
  deps = [
    "//sdk/rust/zx",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-component-test",
    "//src/sys/lib/cm_rust",
    "//src/sys/lib/cm_types",
    "//src/sys/pkg/fidl/fuchsia.update.verify:fuchsia.update.verify_rust",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
  ]

  sources = [ "integration_test.rs" ]
}

component_config("component_manager_config") {
  sources = [
    "//src/sys/component_manager/configs/no_debug.json5",
    "//src/sys/component_manager/configs/test_config.base.json5",
    "health_check.json5",
  ]
  dest = "data/component_manager_config"
}

fuchsia_component("component-manager-cmp") {
  testonly = true
  deps = [
    ":component_manager_config",
    "//src/sys/component_manager:bin_with_tracing",
  ]
  component_name = "component_manager"
  manifest = "//src/sys/component_manager/meta/component_manager.cml"
}

fuchsia_unittest_package("ota-health-verification-test") {
  manifest = "meta/integration_test.cml"
  deps = [
    ":component-manager-cmp",
    ":ota_health_verification_test_bin",
  ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}
