# Copyright 2025 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

executable("test_provider_bin") {
  output_name = "test_provider"

  sources = [ "test_provider.cc" ]

  deps = [
    "//sdk/lib/async-loop:async-loop-cpp",
    "//sdk/lib/async-loop:async-loop-default",
    "//sdk/lib/syslog/cpp",
    "//zircon/system/ulib/trace",
    "//zircon/system/ulib/trace-provider",
  ]
}

fuchsia_component("test_provider") {
  manifest = "meta/test_provider.cml"
  testonly = true
  deps = [ ":test_provider_bin" ]
}

executable("streaming_test_bin") {
  testonly = true
  output_name = "streaming_test"

  sources = [ "streaming_test.cc" ]

  deps = [
    "//sdk/fidl/fuchsia.tracing.controller:fuchsia.tracing.controller_cpp",
    "//sdk/lib/async-loop:async-loop-cpp",
    "//sdk/lib/async-loop:async-loop-default",
    "//sdk/lib/component/incoming/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//zircon/system/ulib/trace-reader",
    "//zircon/system/ulib/zx",
  ]
}
fuchsia_test_component("streaming_test_component") {
  component_name = "streaming_test"
  manifest = "meta/streaming_test.cml"
  deps = [ ":streaming_test_bin" ]
}

fuchsia_test_package("trace_manager_integration_tests") {
  package_name = "trace_manager_integration_tests"
  test_components = [ ":streaming_test_component" ]
  deps = [
    ":test_provider",
    "//src/performance/trace:bin",
    "//src/performance/trace_manager:component_hermetic",
  ]
}
group("tests") {
  testonly = true
  deps = [ ":trace_manager_integration_tests" ]
}
