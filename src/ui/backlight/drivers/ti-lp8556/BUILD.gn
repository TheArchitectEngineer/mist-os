# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/fidl/fidl.gni")
import("//build/test.gni")

driver_bind_rules("ti-lp8556-bind") {
  rules = "meta/ti-lp8556.bind"
  bind_output = "ti-lp8556.bindbc"
  tests = "meta/ti-lp8556-bind-test.json"
  deps = [
    "//sdk/fidl/fuchsia.hardware.i2c:fuchsia.hardware.i2c_bindlib",
    "//src/devices/bind/fuchsia.platform",
    "//src/devices/bind/fuchsia.ti.platform",
  ]
}

source_set("metadata") {
  sources = [ "ti-lp8556Metadata.h" ]
}

source_set("common") {
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [
    "ti-lp8556.cc",
    "ti-lp8556.h",
  ]
  deps = [
    "//sdk/lib/driver/compat/cpp",
    "//sdk/lib/driver/platform-device/cpp",
    "//src/lib/ddk",
    "//src/lib/ddk:ddk_metadata",
    "//zircon/system/ulib/ddk-platform-defs",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/inspect",
    "//zircon/system/ulib/pretty",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zx",
  ]
  public_deps = [
    ":metadata",
    "//sdk/fidl/fuchsia.hardware.backlight:fuchsia.hardware.backlight_cpp",
    "//sdk/fidl/fuchsia.hardware.power.sensor:fuchsia.hardware.power.sensor_cpp",
    "//sdk/lib/driver/component/cpp",
    "//src/devices/i2c/lib/i2c-channel",
    "//src/devices/lib/driver:driver_runtime",
    "//src/devices/lib/mmio",
    "//src/graphics/display/lib/device-protocol-display",
    "//zircon/system/ulib/hwreg",
  ]
}

fuchsia_cc_driver("ti-lp8556-driver") {
  output_name = "ti-lp8556"
  deps = [ ":common" ]

  # TODO(https://fxbug.dev/42136089): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_driver_component("ti-lp8556") {
  component_name = "ti-lp8556"
  deps = [
    ":ti-lp8556-bind",
    ":ti-lp8556-driver",
  ]
  info = "meta/ti-lp8556-info.json"
  manifest = "meta/ti-lp8556.cml"
}

fuchsia_driver_package("package") {
  package_name = "ti-lp8556"
  export_to_bazel = true
  driver_components = [ ":ti-lp8556" ]
}

test("ti-lp8556-test-bin") {
  output_name = "ti-lp8556-test"
  sources = [ "ti-lp8556-test.cc" ]
  deps = [
    ":common",
    "//sdk/lib/driver/compat/cpp",
    "//sdk/lib/driver/fake-platform-device/cpp",
    "//sdk/lib/driver/mock-mmio/cpp",
    "//sdk/lib/driver/testing/cpp",
    "//sdk/lib/inspect/testing/cpp",
    "//src/devices/i2c/testing/mock-i2c:mock-i2c-gtest",
    "//src/devices/testing/mock-mmio-reg",
    "//src/lib/ddk:ddk_metadata",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
    "//zircon/system/ulib/ddk-platform-defs",
  ]

  # TODO(https://fxbug.dev/42136089): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_unittest_package("ti-lp8556-test") {
  package_name = "ti-lp8556-test"
  deps = [ ":ti-lp8556-test-bin" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [
    ":ti-lp8556-bind_test",
    ":ti-lp8556-test",
  ]
}
