# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/dist/resource.gni")
import("//build/json/validate_json.gni")

template("node_config") {
  output_config = "power_manager/node_config.json"
  if (defined(invoker.output)) {
    output_config = invoker.output
  }

  resource("${target_name}_resource") {
    sources = [ "${invoker.source}" ]
    outputs = [ "config/${output_config}" ]
  }

  validate_json5("${target_name}_validate_json") {
    data = "${invoker.source}"
    schema = "//src/power/power-manager/node_config/node_config.schema.json"
  }

  group(target_name) {
    deps = [
      ":${target_name}_resource",
      ":${target_name}_validate_json",
    ]
  }
}
