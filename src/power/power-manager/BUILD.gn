# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_test.gni")
import("//src/power/power-manager/node_config/node_config.gni")
import("//third_party/cobalt/metrics_registry.gni")

metrics_registry("power_manager_metrics") {
  # ID of the 'power' project from the //third_party/cobalt_config/projects.yaml file
  project_id = 3

  # The namespace to use in the generated code
  namespace = "power_manager_metrics"

  # Generates a rust target called 'power_manager_metrics_rustlib'
  generate_rust = true
}

power_manager_deps = [
  ":power-manager-config-lib",
  ":power_manager_metrics_rustlib",
  "system_power_mode_config/parser",
  "thermal_config/parser",
  "//sdk/fidl/fuchsia.feedback:fuchsia.feedback_rust",
  "//sdk/fidl/fuchsia.hardware.power.statecontrol:fuchsia.hardware.power.statecontrol_rust",
  "//sdk/fidl/fuchsia.hardware.temperature:fuchsia.hardware.temperature_rust",
  "//sdk/fidl/fuchsia.metrics:fuchsia.metrics_rust",
  "//sdk/fidl/fuchsia.power.clientlevel:fuchsia.power.clientlevel_rust",
  "//sdk/fidl/fuchsia.power.manager.debug:fuchsia.power.manager.debug_rust",
  "//sdk/fidl/fuchsia.power.profile:fuchsia.power.profile_rust",
  "//sdk/fidl/fuchsia.power.systemmode:fuchsia.power.systemmode_rust",
  "//sdk/fidl/fuchsia.settings:fuchsia.settings_rust",
  "//sdk/fidl/fuchsia.thermal:fuchsia.thermal_rust",
  "//sdk/fidl/fuchsia.ui.activity:fuchsia.ui.activity_rust",
  "//sdk/rust/zx",
  "//src/lib/async-utils",
  "//src/lib/cobalt/rust/builders",
  "//src/lib/diagnostics/inspect/contrib/rust",
  "//src/lib/diagnostics/inspect/runtime/rust",
  "//src/lib/diagnostics/inspect/rust",
  "//src/lib/fake-clock/named-timer",
  "//src/lib/fidl/rust/fidl",
  "//src/lib/fidl/rust/fidl-contrib",
  "//src/lib/fuchsia",
  "//src/lib/fuchsia-async",
  "//src/lib/fuchsia-component",
  "//src/lib/fuchsia-fs",
  "//src/lib/trace/rust:trace",
  "//src/lib/trace/rust:trace-provider",
  "//third_party/rust_crates:anyhow",
  "//third_party/rust_crates:async-trait",
  "//third_party/rust_crates:futures",
  "//third_party/rust_crates:libm",
  "//third_party/rust_crates:log",
  "//third_party/rust_crates:num-traits",
  "//third_party/rust_crates:serde",
  "//third_party/rust_crates:serde_derive",
  "//third_party/rust_crates:serde_json",
  "//third_party/rust_crates:serde_json5",
  "//third_party/rust_crates:thiserror",
]

power_manager_sources = [
  "//src/power/common/lib/common_utils.rs",
  "//src/power/common/lib/types.rs",
  "src/activity_handler.rs",
  "src/crash_report_handler.rs",
  "src/debug_service.rs",
  "src/error.rs",
  "src/input_settings_handler.rs",
  "src/main.rs",
  "src/message.rs",
  "src/node.rs",
  "src/platform_metrics.rs",
  "src/power_manager.rs",
  "src/system_power_mode_handler.rs",
  "src/system_profile_handler.rs",
  "src/system_shutdown_handler.rs",
  "src/temperature_handler.rs",
  "src/test/mock_node.rs",
  "src/test/mod.rs",
  "src/thermal_load_driver.rs",
  "src/thermal_policy.rs",
  "src/thermal_shutdown.rs",
  "src/thermal_state_handler.rs",
  "src/timer.rs",
  "src/utils.rs",
]

rustc_binary("bin") {
  name = "power_manager"
  edition = "2021"
  with_unit_tests = true
  configs += [ "//build/config/rust:bootfs" ]
  deps = power_manager_deps
  sources = power_manager_sources
  test_deps = [
    "//src/lib/diagnostics/testing/diagnostics-assertions/rust:diagnostics-assertions",
    "//third_party/rust_crates:assert_matches",
  ]
}

rustc_binary("bin_with_fake_time") {
  testonly = true
  name = "power_manager_fake_time"
  edition = "2021"
  with_unit_tests = true
  configs += [ "//build/config/rust:bootfs" ]
  deps = power_manager_deps
  sources = power_manager_sources
  non_rust_deps = [ "//src/lib/fake-clock/lib" ]
}

fuchsia_component_manifest("manifest") {
  manifest = "meta/power-manager.cml"
  component_name = "power-manager"
}

declare_args() {
  enable_power_manager_debug = false
}

fuchsia_structured_config_values("default_component_config") {
  cm_label = ":manifest"
  values = {
    enable_debug_service = enable_power_manager_debug

    # Below are default values that are only expected to be changed for integration tests.
    node_config_path = "/config/node_config.json"
    disable_temperature_filter = false
  }
}

fuchsia_structured_config_rust_lib("power-manager-config-lib") {
  cm_label = ":manifest"
}

group("power-manager") {
  testonly = true
  deps = [
    ":bin",
    ":tests",
  ]
}

fuchsia_component("component") {
  cm_label = ":manifest"
  deps = [ ":bin" ]
}

fuchsia_package("package") {
  package_name = "power-manager"
  deps = [
    ":component",
    ":default_component_config",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":power-manager-tests",
    "integration:tests",
    "rkf45:rkf45-tests",
    "system_power_mode_config:tests",
    "testing/fake_root:tests",
    "thermal_config:tests",
  ]
}

node_config("astro") {
  source = "node_config/astro_node_config.json5"
  output = "astro_node_config.json5"
}

node_config("base") {
  source = "node_config/base_node_config.json5"
  output = "base_node_config.json5"
}

node_config("nelson") {
  source = "node_config/nelson_node_config.json5"
  output = "nelson_node_config.json5"
}

node_config("sherlock") {
  source = "node_config/sherlock_node_config.json5"
  output = "sherlock_node_config.json5"
}

node_config("vim3_devicetree") {
  source = "node_config/vim3_devicetree_node_config.json5"
  output = "vim3_devicetree_node_config.json5"
}

fuchsia_unittest_package("power-manager-tests") {
  manifest = "meta/power_manager_bin_test.cml"
  deps = [
    ":astro",
    ":base",
    ":bin_test",
    ":nelson",
    ":sherlock",
    ":vim3_devicetree",
  ]
}
