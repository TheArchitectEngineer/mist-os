# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("fake_battery_bind") {
  rules = "meta/fake_battery_driver.bind"
  bind_output = "fake_battery_driver.bindbc"
  deps = [ "//src/devices/bind/fuchsia.test" ]
  deps += [ "//src/devices/bind/fuchsia.platform" ]
}

source_set("common") {
  sources = [
    "driver.cc",
    "driver.h",
    "power_source_protocol_server.cc",
    "power_source_protocol_server.h",
    "power_source_state.cc",
    "power_source_state.h",
    "simulator_impl.cc",
    "simulator_impl.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.hardware.powersource:fuchsia.hardware.powersource_cpp",
    "//sdk/fidl/fuchsia.hardware.powersource.test:fuchsia.hardware.powersource.test_cpp",
    "//sdk/fidl/fuchsia.power.battery:fuchsia.power.battery_cpp",
    "//sdk/lib/driver/component/cpp",
    "//sdk/lib/driver/devfs/cpp",
    "//src/devices/lib/driver:driver_runtime",
  ]
}

fuchsia_cc_driver("fake_battery_driver") {
  output_name = "fake_battery"
  deps = [ ":common" ]
}

fuchsia_driver_component("fake_battery_component") {
  component_name = "fake_battery"
  manifest = "meta/fake_battery_driver.cml"
  deps = [
    ":fake_battery_bind",
    ":fake_battery_driver",
  ]
  info = "meta/component_info.json"
}

fuchsia_driver_package("fake_battery") {
  driver_components = [ ":fake_battery_component" ]
}

test("fake_battery_test_bin") {
  output_name = "fake_battery_driver_unittests"
  sources = [ "fake_battery_driver_unittest.cc" ]

  deps = [
    ":common",
    "//sdk/lib/driver/testing/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
  ]
}

fuchsia_unittest_package("fake_battery_driver_tests") {
  deps = [ ":fake_battery_test_bin" ]
}

test("driver_integration_test") {
  sources = [ "integration_test.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.driver.test:fuchsia.driver.test_cpp",
    "//sdk/fidl/fuchsia.hardware.powersource:fuchsia.hardware.powersource_cpp",
    "//sdk/fidl/fuchsia.hardware.powersource.test:fuchsia.hardware.powersource.test_cpp",
    "//sdk/fidl/fuchsia.power.battery:fuchsia.power.battery_cpp",
    "//sdk/lib/component/incoming/cpp",
    "//sdk/lib/device-watcher/cpp",
    "//sdk/lib/driver_test_realm/realm_builder/cpp",
    "//sdk/lib/syslog/cpp",
    "//src/devices/bind/fuchsia.platform:fuchsia.platform_cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
  ]
}

fuchsia_unittest_package("integration_tests") {
  package_name = "fake_battery_integration_test"
  deps = [
    # Test component.
    ":driver_integration_test",

    # Driver(s) to be tested.
    ":fake_battery_component",

    # Include the platform bus (if driver binds to it)
    "//src/devices/bus/drivers/platform:platform-bus",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":fake_battery_driver_tests",
    ":integration_tests",
  ]
}
