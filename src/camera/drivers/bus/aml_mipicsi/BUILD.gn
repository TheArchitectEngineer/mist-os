# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

group("tests") {
  testonly = true
  deps = [
    ":aml_mipi_bind_test",
    ":aml_mipi_test_package",
  ]
}

driver_bind_rules("aml_mipi_bind") {
  rules = "meta/aml_mipi.bind"
  tests = "meta/bind_tests.json"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.platform",
  ]
}

source_set("aml_mipi_driver_source") {
  sources = [
    "aml_mipi.cc",
    "aml_mipi.h",
    "aml_mipi_adap.cc",
    "aml_mipi_debug.cc",
    "aml_mipi_regs.h",
  ]
  deps = [ "//src/devices/lib/driver" ]
  public_deps = [
    "//sdk/banjo/fuchsia.hardware.mipicsi:fuchsia.hardware.mipicsi_banjo_cpp",
    "//sdk/lib/driver/platform-device/cpp",
    "//src/devices/lib/mmio",
    "//src/lib/ddk",
    "//src/lib/ddk:ddk-metadata-headers",
    "//src/lib/ddktl",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/zx",
    "//zircon/third_party/ulib/safemath",
  ]
}

fuchsia_cc_driver("aml_mipicsi_driver-driver") {
  legacy = true
  output_name = "aml_mipicsi"
  deps = [
    ":aml_mipi_bind",
    ":aml_mipi_driver_source",
  ]
}

fuchsia_driver_component("aml_mipicsi_driver") {
  component_name = "aml_mipicsi"
  deps = [ ":aml_mipicsi_driver-driver" ]
  info = "meta/aml_mipicsi_driver-info.json"
  manifest = "meta/aml_mipicsi_driver.cml"
}

fuchsia_driver_package("driver_package") {
  package_name = "aml-mipicsi"
  export_to_bazel = true
  driver_components = [ ":aml_mipicsi_driver" ]
}

test("aml_mipi_test") {
  sources = [ "aml_mipi_test.cc" ]
  deps = [
    ":aml_mipi_driver_source",
    "//sdk/lib/async-loop:async-loop-cpp",
    "//sdk/lib/async-loop:async-loop-default",
    "//sdk/lib/async_patterns/testing/cpp",
    "//sdk/lib/component/outgoing/cpp",
    "//sdk/lib/driver/fake-platform-device/cpp",
    "//src/devices/testing/fake-mmio-reg",
    "//src/devices/testing/mock-ddk",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("aml_mipi_test_package") {
  deps = [ ":aml_mipi_test" ]
}

group("aml_mipicsi") {
  deps = [ ":driver_package" ]
}
