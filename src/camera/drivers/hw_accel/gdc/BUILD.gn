# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/testing/cc_test_executable.gni")
import("//src/camera/debug.gni")

group("tests") {
  testonly = true
  deps = [
    ":gdc_bind_test",
    ":gdc_task_unittest",
  ]
}

driver_bind_rules("gdc_bind") {
  rules = "meta/gdc.bind"
  tests = "meta/bind_tests.json"
  deps = [
    "//src/devices/bind/fuchsia.arm.platform",
    "//src/devices/bind/fuchsia.camera",
    "//src/devices/bind/fuchsia.platform",
  ]
}

source_set("lib") {
  defines = [ camera_debug_define ]
  visibility = [ "../*" ]

  sources = [
    "gdc.cc",
    "gdc.h",
    "gdc_task.cc",
    "gdc_task.h",
  ]

  deps = [ "//sdk/lib/driver/platform-device/cpp" ]

  public_deps = [
    ":gdc_bind",
    "//sdk/banjo/fuchsia.hardware.gdc:fuchsia.hardware.gdc_banjo_cpp",
    "//sdk/lib/async:async-cpp",
    "//sdk/lib/async-loop:async-loop-cpp",
    "//sdk/lib/async-loop:async-loop-default",
    "//sdk/lib/fit",
    "//src/camera/drivers/hw_accel/task:camera-task",
    "//src/devices/lib/driver",
    "//src/devices/lib/mmio",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/hwreg",
    "//zircon/system/ulib/image-format",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/trace",
    "//zircon/system/ulib/trace",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_cc_driver("fuchsia_driver") {
  legacy = true
  output_name = "gdc"
  deps = [ ":lib" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
}

fuchsia_driver_component("driver") {
  component_name = "gdc"
  deps = [ ":fuchsia_driver" ]
  info = "meta/driver-info.json"
  manifest = "meta/driver.cml"
}

fuchsia_driver_package("gdc") {
  export_to_bazel = true
  driver_components = [ ":driver" ]
}

cc_test_executable("gdc_task_unittest_bin") {
  testonly = true
  sources = [ "gdc_task_unittest.cc" ]
  deps = [
    ":lib",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_cpp",
    "//sdk/lib/driver/mock-mmio/cpp",
    "//sdk/lib/zbi-format",
    "//src/camera/drivers/test_utils",
    "//src/devices/lib/driver",
    "//src/devices/lib/mmio",
    "//src/devices/testing/fake-bti",
    "//src/devices/testing/no_ddk",
    "//src/lib/fsl",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("gdc_task_unittest") {
  deps = [ ":gdc_task_unittest_bin" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}
