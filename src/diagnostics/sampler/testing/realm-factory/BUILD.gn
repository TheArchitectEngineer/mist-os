# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

rustc_binary("bin") {
  testonly = true
  edition = "2021"
  name = "sampler_realm_factory"
  sources = [
    "src/main.rs",
    "src/mocks.rs",
    "src/realm_factory.rs",
  ]
  deps = [
    "../fidl:test.sampler_rust",
    "//sdk/fidl/fuchsia.component:fuchsia.component_rust",
    "//sdk/fidl/fuchsia.component.sandbox:fuchsia.component.sandbox_rust",
    "//sdk/fidl/fuchsia.diagnostics:fuchsia.diagnostics_rust",
    "//sdk/fidl/fuchsia.hardware.power.statecontrol:fuchsia.hardware.power.statecontrol_rust",
    "//sdk/fidl/fuchsia.inspect:fuchsia.inspect_rust",
    "//sdk/fidl/fuchsia.logger:fuchsia.logger_rust",
    "//sdk/fidl/fuchsia.metrics:fuchsia.metrics_rust",
    "//sdk/fidl/fuchsia.metrics.test:fuchsia.metrics.test_rust",
    "//src/diagnostics/lib/sampler-config",
    "//src/diagnostics/sampler/tests/fidl:fuchsia.mockrebootcontroller_rust",
    "//src/diagnostics/sampler/tests/fidl:fuchsia.samplertestcontroller_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-component-test",
    "//src/lib/sandbox",
    "//src/sys/lib/cm_rust",
    "//src/sys/lib/component_id_index",
    "//src/sys/lib/moniker",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
    "//third_party/rust_crates:serde_json5",
  ]
  configs += [ "//build/config/rust/lints:clippy_warn_all" ]

  inputs = [
    "configs/reboot_required_config.json",
    "configs/test_config.json",
    "configs/components.json5",
    "configs/fire_1.json5",
    "configs/fire_2.json5",
    "configs/fire_3.json5",
  ]
}

fuchsia_component("realm-factory-component") {
  testonly = true
  component_name = "realm-factory"
  manifest = "meta/default.cml"
  deps = [ ":bin" ]
}

fuchsia_structured_config_values("sampler-test-config") {
  values_source = "sampler-test-config.json5"
  component_name = "sampler"
  cm_label = "//src/diagnostics/sampler:sampler-manifest"
}

fuchsia_package("realm-factory") {
  package_name = "sampler-realm-factory"
  testonly = true
  deps = [
    ":realm-factory-component",
    ":sampler-test-config",
    "//src/cobalt/bin/testing/fake_cobalt:component_v2",
    "//src/diagnostics/archivist:archivist-for-embedding-bundle",
    "//src/diagnostics/sampler:sampler-component",
    "//src/diagnostics/sampler/tests/test_component:single_counter_test_component",
  ]
}
