# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_library.gni")

# Integration test binary for sampler service.
rustc_test("integration-bin") {
  edition = "2021"
  testonly = true
  name = "sampler_integration_test"
  deps = [
    "../testing/fidl:test.sampler_rust",
    "//sdk/fidl/fuchsia.component:fuchsia.component_rust",
    "//sdk/fidl/fuchsia.diagnostics:fuchsia.diagnostics_rust",
    "//sdk/fidl/fuchsia.metrics:fuchsia.metrics_rust",
    "//sdk/fidl/fuchsia.metrics.test:fuchsia.metrics.test_rust",
    "//sdk/rust/zx",
    "//src/diagnostics/sampler/tests/fidl:fuchsia.mockrebootcontroller_rust",
    "//src/diagnostics/sampler/tests/fidl:fuchsia.samplertestcontroller_rust",
    "//src/lib/diagnostics/reader/rust",
    "//src/lib/diagnostics/testing/diagnostics-assertions/rust:diagnostics-assertions",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/testing/realm_client/rust",
    "//third_party/rust_crates:anyhow",
  ]

  source_root = "lib.rs"

  sources = [
    "lib.rs",
    "test_topology.rs",
    "utils.rs",
  ]

  configs += [ "//build/config/rust/lints:clippy_warn_all" ]
}

fuchsia_component("sampler-test-suite-component") {
  testonly = true
  component_name = "sampler-test-suite"
  manifest = "meta/integration-test.cml"
  deps = [ ":integration-bin" ]
}

fuchsia_package("sampler-test-suite") {
  testonly = true
  deps = [ ":sampler-test-suite-component" ]
}

fuchsia_component("sampler-test-root") {
  testonly = true
  manifest = "meta/test-root.cml"
}

fuchsia_test_package("sampler-integration-tests") {
  test_components = [ ":sampler-test-root" ]
  subpackages = [
    "../testing/realm-factory",
    ":sampler-test-suite",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":sampler-integration-tests" ]
}
