# Copyright 2025 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("bind") {
  rules = "meta/interconnect.bind"
  bind_output = "interconnect.bindbc"
  deps = [ "//sdk/fidl/fuchsia.hardware.interconnect:fuchsia.hardware.interconnect_bindlib" ]
}

fuchsia_rust_driver("driver") {
  edition = "2024"
  output_name = "interconnect"
  with_unit_tests = true

  sources = [
    "src/graph.rs",
    "src/lib.rs",
  ]
  deps = [
    "//sdk/fidl/fuchsia.driver.framework:fuchsia.driver.framework_rust",
    "//sdk/fidl/fuchsia.hardware.interconnect:fuchsia.hardware.interconnect_rust",
    "//sdk/lib/driver/component/rust",
    "//sdk/lib/driver/runtime/rust",
    "//sdk/rust/zx",
    "//src/devices/bind/fuchsia:fuchsia_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-sync",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
  ]

  test_deps = [ "//src/lib/fuchsia" ]
}

fuchsia_driver_component("component") {
  component_name = "interconnect"
  manifest = "meta/interconnect.cml"
  deps = [
    ":bind",
    ":driver",
  ]
  info = "meta/component-info.json"
}

fuchsia_driver_package("package") {
  package_name = "interconnect"
  driver_components = [ ":component" ]
}

fuchsia_unittest_package("unit-test-package") {
  package_name = "interconnect-test"
  deps = [ ":driver_test" ]

  # There are expected error logs in test cases checking for parsing errors.
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

driver_bind_rules("fake_bind") {
  rules = "meta/fake_interconnect.bind"
  bind_output = "fake_interconnect.bindbc"
  deps = [ "//sdk/fidl/fuchsia.hardware.interconnect:fuchsia.hardware.interconnect_bindlib" ]
}

fuchsia_rust_driver("fake_driver") {
  edition = "2024"
  output_name = "fake_interconnect"

  source_root = "fake/lib.rs"
  sources = [ "fake/lib.rs" ]
  deps = [
    "//sdk/fidl/fuchsia.driver.framework:fuchsia.driver.framework_rust",
    "//sdk/fidl/fuchsia.hardware.interconnect:fuchsia.hardware.interconnect_rust",
    "//sdk/lib/driver/component/rust",
    "//sdk/lib/driver/runtime/rust",
    "//sdk/rust/zx",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
  ]
}

fuchsia_driver_component("fake_component") {
  component_name = "fake_interconnect"
  manifest = "meta/fake_interconnect.cml"
  deps = [
    ":fake_bind",
    ":fake_driver",
  ]
  info = "meta/component-info.json"
}

rustc_test("integration-test") {
  output_name = "interconnect-integration-test"
  edition = "2024"
  testonly = true
  source_root = "integration_test.rs"
  sources = [ "integration_test.rs" ]
  deps = [
    "//sdk/fidl/fuchsia.driver.development:fuchsia.driver.development_rust",
    "//sdk/fidl/fuchsia.driver.framework:fuchsia.driver.framework_rust",
    "//sdk/fidl/fuchsia.driver.test:fuchsia.driver.test_rust",
    "//sdk/lib/driver_test_realm/realm_builder/rust",
    "//src/devices/bind/fuchsia:fuchsia_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component-test",
    "//third_party/rust_crates:anyhow",
  ]
}

fuchsia_unittest_package("integration-test-package") {
  package_name = "interconnect-integration-test"
  deps = [
    ":component",
    ":fake_component",
    ":integration-test",
  ]

  # There's expected error logs that happen due to races in driver enumeration.
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [
    ":integration-test-package",
    ":unit-test-package",
  ]
}
