# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("virtio_socket_bind") {
  rules = "meta/virtio_socket.bind"
  tests = "meta/bind_tests.json"
  deps = [
    "//sdk/fidl/fuchsia.hardware.pci:fuchsia.hardware.pci_bindlib",
    "//src/devices/bind/fuchsia.acpi",
    "//src/devices/bind/fuchsia.pci",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":test_package",
    ":virtio_socket_bind_test",
  ]
}

source_set("common") {
  sources = [ "socket.cc" ]
  public_deps = [
    "//sdk/fidl/fuchsia.hardware.vsock:fuchsia.hardware.vsock_cpp",
    "//sdk/lib/async:async-cpp",
    "//sdk/lib/async-loop:async-loop-cpp",
    "//sdk/lib/async-loop:async-loop-default",
    "//src/devices/bin/driver_runtime",
    "//src/devices/bus/lib/virtio:virtio-dfv1",
    "//src/devices/lib/driver",
    "//src/lib/ddktl",
  ]
}

fuchsia_cc_driver("virtio_socket-driver") {
  legacy = true
  output_name = "virtio_socket"
  sources = [ "socket_driver.cc" ]
  deps = [
    ":common",
    ":virtio_socket_bind",
  ]
}

fuchsia_driver_component("virtio_socket") {
  info = "meta/virtio_socket-info.json"
  component_name = "virtio_socket"
  manifest = "meta/virtio_socket.cml"
  deps = [ ":virtio_socket-driver" ]
}

fuchsia_driver_package("package") {
  package_name = "virtio_socket"
  driver_components = [ ":virtio_socket" ]
  export_to_bazel = true
}

test("test") {
  output_name = "virtio-socket-test"
  sources = [ "test.cc" ]
  deps = [
    ":common",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("test_package") {
  package_name = "virtio-socket-tests"
  deps = [ ":test" ]
}
