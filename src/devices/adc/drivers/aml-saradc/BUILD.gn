# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("aml_saradc_bind") {
  rules = "meta/aml-saradc.bind"
  bind_output = "aml-saradc.bindbc"
  tests = "meta/test_bind.json"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.devicetree",
    "//src/devices/bind/fuchsia.platform",
  ]
}

source_set("common") {
  sources = [
    "aml-saradc.cc",
    "aml-saradc.h",
  ]
  public_deps = [
    ":aml_saradc_bind",
    "//sdk/fidl/fuchsia.hardware.adcimpl:fuchsia.hardware.adcimpl_cpp",
    "//sdk/lib/driver/compat/cpp",
    "//sdk/lib/driver/component/cpp",
    "//sdk/lib/driver/metadata/cpp",
    "//sdk/lib/driver/platform-device/cpp",
    "//src/devices/lib/amlogic",
    "//src/devices/lib/mmio",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_cc_driver("aml-saradc-driver") {
  output_name = "aml-saradc"
  deps = [
    ":common",
    "//src/devices/lib/driver:driver_runtime",
  ]
}

fuchsia_driver_component("aml-saradc") {
  info = "meta/aml-saradc-info.json"
  component_name = "aml-saradc"
  deps = [ ":aml-saradc-driver" ]
  manifest = "meta/aml-saradc.cml"
}

fuchsia_driver_package("package") {
  package_name = "aml-saradc"
  export_to_bazel = true
  driver_components = [ ":aml-saradc" ]
}

test("aml-saradc-test-bin") {
  output_name = "aml-saradc-test"
  sources = [ "aml-saradc-test.cc" ]
  deps = [
    ":common",
    "//sdk/lib/driver/fake-mmio-reg/cpp",
    "//sdk/lib/driver/fake-platform-device/cpp",
    "//sdk/lib/driver/testing/cpp",
    "//src/lib/fxl/test:gtest_main",
  ]
}

fuchsia_unittest_package("aml-saradc-test") {
  deps = [ ":aml-saradc-test-bin" ]
}

group("tests") {
  testonly = true
  deps = [
    ":aml-saradc-test",
    ":aml_saradc_bind_test",
  ]
}
