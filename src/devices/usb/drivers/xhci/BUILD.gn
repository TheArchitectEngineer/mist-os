# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")

driver_bind_rules("usb_xhci_bind") {
  rules = "meta/usb_xhci.bind"
  bind_output = "xhci.bindbc"
  tests = "meta/tests.json"
  deps = [
    "//sdk/fidl/fuchsia.hardware.pci:fuchsia.hardware.pci_bindlib",
    "//sdk/fidl/fuchsia.hardware.platform.device:fuchsia.hardware.platform.device_bindlib",
    "//sdk/fidl/fuchsia.hardware.usb.phy:fuchsia.hardware.usb.phy_bindlib",
    "//src/devices/bind/fuchsia.acpi",
    "//src/devices/bind/fuchsia.devicetree",
    "//src/devices/bind/fuchsia.pci",
    "//src/devices/bind/fuchsia.pci:serial",
    "//src/devices/bind/fuchsia.pci:serial.usb",
    "//src/devices/bind/fuchsia.platform",
    "//src/devices/bind/fuchsia.usb.phy",
  ]
}

source_set("common") {
  public_deps = [
    ":usb_xhci_bind",
    ":xhci-config",
    "//sdk/banjo/fuchsia.hardware.usb.hci:fuchsia.hardware.usb.hci_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.usb.phy:fuchsia.hardware.usb.phy_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.usb.hci:fuchsia.hardware.usb.hci_cpp",
    "//sdk/fidl/fuchsia.power.system:fuchsia.power.system_cpp",
    "//sdk/lib/async-loop:async-loop-cpp",
    "//sdk/lib/driver/compat/cpp",
    "//sdk/lib/driver/component/cpp:cpp",
    "//sdk/lib/driver/platform-device/cpp",
    "//sdk/lib/driver/power/cpp",
    "//sdk/lib/fit-promise",
    "//src/devices/lib/dma-buffer",
    "//src/devices/lib/mmio",
    "//src/devices/lib/synchronous-executor",
    "//src/devices/pci/lib/device-protocol-pci",
    "//src/devices/usb/lib/usb:usb-dfv2-compat-logging",
    "//src/devices/usb/lib/usb-endpoint:usb-endpoint-server-dfv2-compat-logging",
    "//src/devices/usb/lib/usb-phy:usb-phy-dfv2-compat-logging",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/hwreg",
    "//zircon/system/ulib/inspect",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_cc_driver("xhci-driver") {
  output_name = "xhci"

  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [
    "usb-xhci.cc",
    "xhci-device-state.cc",
    "xhci-endpoint.cc",
    "xhci-enumeration.cc",
    "xhci-event-ring.cc",
    "xhci-interrupter.cc",
    "xhci-transfer-ring.cc",
  ]
  deps = [
    ":common",
    "//src/devices/lib/driver:driver_runtime",
  ]
}

fuchsia_component_manifest("xhci-manifest") {
  component_name = "xhci"
  manifest = "meta/xhci.cml"
}

fuchsia_driver_component("xhci") {
  info = "meta/xhci-info.json"
  cm_label = ":xhci-manifest"
  deps = [ ":xhci-driver" ]
}

fuchsia_structured_config_cpp_elf_lib("xhci-config") {
  cm_label = ":xhci-manifest"
}

fuchsia_structured_config_values("xhci-values") {
  cm_label = ":xhci-manifest"
  values = {
    enable_suspend = false
  }
}

fuchsia_driver_package("package") {
  package_name = "xhci"
  driver_components = [ ":xhci" ]
  deps = [ ":xhci-values" ]
}

group("tests") {
  testonly = true
  deps = [
    ":usb_xhci_bind_test",
    "tests:unittests",
  ]
}
