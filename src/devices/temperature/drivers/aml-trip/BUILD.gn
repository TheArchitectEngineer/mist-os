# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")

driver_bind_rules("aml-trip-bind") {
  rules = "meta/aml-trip.bind"
  bind_output = "aml-trip.bindbc"
  tests = "meta/aml-trip-bind-test.json"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.clock",
    "//src/devices/bind/fuchsia.devicetree",
    "//src/devices/bind/fuchsia.platform",
  ]
}

source_set("common") {
  sources = [
    "aml-trip-device.cc",
    "aml-trip-device.h",
    "aml-trip.cc",
    "aml-trip.h",
    "aml-tsensor-regs.h",
    "util.cc",
    "util.h",
  ]
  deps = [
    "//sdk/fidl/fuchsia.driver.compat:fuchsia.driver.compat_cpp",
    "//sdk/fidl/fuchsia.hardware.clock:fuchsia.hardware.clock_cpp",
    "//sdk/fidl/fuchsia.hardware.platform.bus:fuchsia.hardware.platform.bus_cpp",
    "//sdk/fidl/fuchsia.hardware.platform.device:fuchsia.hardware.platform.device_cpp",
    "//sdk/lib/driver/compat/cpp",
    "//sdk/lib/driver/component/cpp:cpp",
    "//sdk/lib/driver/logging/cpp:cpp",
    "//sdk/lib/driver/platform-device/cpp",
    "//sdk/lib/driver/runtime:driver_runtime_cpp",
    "//src/lib/ddk:ddk_metadata",
    "//zircon/system/ulib/ddk-platform-defs",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/hwreg",
  ]
  public_deps = [
    "//sdk/fidl/fuchsia.hardware.trippoint:fuchsia.hardware.trippoint_cpp",
    "//sdk/lib/driver/devfs/cpp",
    "//src/devices/lib/mmio",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_cc_driver("aml-trip-driver") {
  output_name = "aml-trip"
  deps = [
    ":aml-trip-bind",
    ":common",
  ]
}

fuchsia_driver_component("aml-trip") {
  component_name = "aml-trip"
  deps = [ ":aml-trip-driver" ]
  info = "meta/aml-trip-info.json"
  manifest = "meta/aml-trip.cml"
}

fuchsia_driver_package("package") {
  package_name = "aml-trip"
  driver_components = [ ":aml-trip" ]
}

group("tests") {
  testonly = true
  deps = [
    ":aml-trip-bind_test",
    "tests:aml-trip-test-package",
  ]
}
