# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("aml_nna_bind") {
  rules = "meta/aml_nna.bind"
  bind_output = "aml-nna.bindbc"
  tests = "meta/bind_tests.json"
  deps = [
    "//sdk/fidl/fuchsia.hardware.registers:fuchsia.hardware.registers_bindlib",
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.devicetree",
    "//src/devices/bind/fuchsia.platform",
  ]
}

source_set("common") {
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [
    "a5-nna-regs.h",
    "aml-nna.cc",
    "aml-nna.h",
    "s905d3-nna-regs.h",
    "t931-nna-regs.h",
  ]
  deps = [
    "//src/devices/bind/fuchsia:fuchsia_cpp",
    "//src/devices/bind/fuchsia.platform:fuchsia.platform_cpp",
    "//src/devices/bind/fuchsia.verisilicon.platform:fuchsia.verisilicon.platform_cpp",
    "//zircon/system/ulib/fbl",
  ]
  public_deps = [
    "//sdk/fidl/fuchsia.hardware.registers:fuchsia.hardware.registers_cpp",
    "//sdk/lib/component/outgoing/cpp",
    "//sdk/lib/driver/platform-device/cpp",
    "//src/devices/lib/amlogic",
    "//src/devices/lib/driver:driver",
    "//src/devices/lib/driver:driver_runtime",
    "//src/devices/lib/mmio",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/hwreg",
  ]
}

fuchsia_cc_driver("aml-nna-driver") {
  legacy = true
  output_name = "aml-nna"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  deps = [
    ":aml_nna_bind",
    ":common",
  ]
}

fuchsia_driver_component("aml-nna") {
  component_name = "aml-nna"
  deps = [ ":aml-nna-driver" ]
  info = "meta/aml-nna-info.json"
  manifest = "meta/aml-nna.cml"
}

fuchsia_driver_package("package") {
  package_name = "aml-nna"
  export_to_bazel = true
  driver_components = [ ":aml-nna" ]
}

test("aml-nna-test-bin") {
  output_name = "aml-nna-test"
  sources = [ "aml-nna-test.cc" ]
  deps = [
    ":common",
    "//sdk/lib/async-loop:async-loop-cpp",
    "//src/devices/registers/testing/mock-registers",
    "//src/devices/testing/mock-ddk",
    "//src/devices/testing/mock-mmio-reg:mock-mmio-reg-zxtest",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("aml-nna-test") {
  deps = [ ":aml-nna-test-bin" ]
  test_specs = {
    environments = basic_envs
  }
}

group("tests") {
  testonly = true
  deps = [
    ":aml-nna-test",
    ":aml_nna_bind_test",
  ]
}
