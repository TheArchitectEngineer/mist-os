# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/drivers.gni")

driver_bind_rules("ramdisk_bind") {
  rules = "meta/ramdisk.bind"
  bind_output = "ramdisk.bindbc"
  tests = "meta/bind_tests.json"
  deps = [ "//src/devices/bind/fuchsia.platform" ]
}

fuchsia_cc_driver("ramdisk-driver") {
  legacy = true
  output_name = "ramdisk"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [
    "v1/ramdisk-controller.cc",
    "v1/ramdisk.cc",
  ]
  deps = [
    ":fuchsia.hardware.ramdisk_cpp",
    ":ramdisk_bind",
    "//sdk/banjo/fuchsia.hardware.block.partition:fuchsia.hardware.block.partition_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.block.driver:fuchsia.hardware.block.driver_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.block.volume:fuchsia.hardware.block.volume_cpp",
    "//sdk/fidl/fuchsia.hardware.ramdisk:fuchsia.hardware.ramdisk_cpp",
    "//sdk/lib/zbi-format",
    "//src/devices/block/lib/common:common-dfv1",
    "//src/devices/lib/dev-operation",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_driver_component("ramdisk-v1") {
  visibility = [ ":*" ]
  component_name = "ramdisk"
  deps = [ ":ramdisk-driver" ]
  info = "meta/ramdisk-info.json"
  manifest = "meta/ramdisk.cml"
}

bind_library("fuchsia.hardware.ramdisk") {
  source = "ramdisk-lib.bind"
}

driver_bind_rules("ramdisk_v2_bind") {
  rules = "ramdisk-v2.bind"
  bind_output = "ramdisk-v2.bindbc"
  tests = "bind_tests_v2.json"
  deps = [ ":fuchsia.hardware.ramdisk" ]
}

fuchsia_cc_driver("ramdisk-driver-v2") {
  output_name = "ramdisk-v2"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [
    "v2/ramdisk-controller.cc",
    "v2/ramdisk.cc",
  ]
  deps = [
    ":ramdisk_v2_bind",
    "//sdk/fidl/fuchsia.hardware.ramdisk:fuchsia.hardware.ramdisk_cpp",
    "//sdk/fidl/fuchsia.io:fuchsia.io_cpp",
    "//sdk/lib/driver/component/cpp",
    "//sdk/lib/zbi-format",
    "//src/devices/lib/driver:driver_runtime",
    "//src/storage/lib/block_server:block_server_cc",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zx",
    "//zircon/third_party/ulib/safemath",
  ]
}

fuchsia_driver_component("ramdisk-v2") {
  visibility = [ ":*" ]
  component_name = "ramdisk-v2"
  deps = [ ":ramdisk-driver-v2" ]
  info = "meta/ramdisk-info.json"
  manifest = "meta/ramdisk-v2.cml"
}

fuchsia_driver_package("package") {
  package_name = "ramdisk"
  driver_components = [
    ":ramdisk",
    ":ramdisk-v2",
  ]
}

group("ramdisk") {
  deps = [
    ":ramdisk-v1",
    ":ramdisk-v2",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":ramdisk_bind_test",
    ":ramdisk_v2_bind_test",
    "test:tests",
  ]
}
