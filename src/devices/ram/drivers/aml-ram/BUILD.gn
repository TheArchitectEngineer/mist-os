# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("aml_ram_bind") {
  rules = "meta/aml_ram.bind"
  bind_output = "aml-ram.bindbc"
  tests = "meta/bind_tests.json"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.platform",
  ]
}

fuchsia_cc_driver("aml-ram-driver") {
  legacy = true
  output_name = "aml-ram"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [
    "aml-ram-hardware-test.cc",
    "aml-ram.cc",
  ]
  deps = [
    ":aml_ram_bind",
    ":common",
    "//src/devices/lib/driver",
    "//src/devices/lib/mmio",
  ]
}

fuchsia_driver_component("aml-ram") {
  info = "meta/aml-ram-info.json"
  component_name = "aml-ram"
  deps = [ ":aml-ram-driver" ]
  manifest = "meta/aml-ram.cml"
}

fuchsia_driver_package("package") {
  package_name = "aml-ram"
  export_to_bazel = true
  driver_components = [ ":aml-ram" ]
}

test("aml-ram-test-bin") {
  output_name = "aml-ram-test"
  sources = [
    "aml-ram-test.cc",
    "aml-ram.cc",
  ]
  deps = [
    ":aml_ram_bind",
    ":common",
    "//sdk/lib/async-loop:async-loop-cpp",
    "//sdk/lib/async-loop:async-loop-default",
    "//sdk/lib/async_patterns/testing/cpp",
    "//sdk/lib/component/outgoing/cpp",
    "//sdk/lib/driver/fake-mmio-reg/cpp",
    "//sdk/lib/driver/fake-platform-device/cpp",
    "//src/devices/testing/mock-ddk",
    "//zircon/system/ulib/mock-function",
  ]
}

group("common") {
  public_deps = [
    "//sdk/fidl/fuchsia.device:fuchsia.device_cpp",
    "//sdk/fidl/fuchsia.hardware.ram.metrics:fuchsia.hardware.ram.metrics_cpp",
    "//sdk/lib/driver/platform-device/cpp",
    "//src/devices/lib/amlogic",
    "//src/devices/lib/driver:driver_runtime",
    "//src/devices/lib/mmio",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/driver-unit-test",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/hwreg",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("aml-ram-test") {
  deps = [ ":aml-ram-test-bin" ]
  test_specs = {
    environments = basic_envs
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [
    ":aml-ram-test",
    ":aml_ram_bind_test",
  ]
}
