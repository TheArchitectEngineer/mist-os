# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/drivers.gni")

driver_bind_rules("ftdi_bind") {
  rules = "meta/ftdi.bind"
  bind_output = "ftdi.bindbc"
  tests = "meta/tests.json"
  deps = [ "//src/devices/bind/fuchsia.usb" ]
}

source_set("common") {
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [
    "ftdi-i2c.cc",
    "ftdi-i2c.h",
    "ftdi-mpsse.cc",
    "ftdi-mpsse.h",
    "ftdi.cc",
    "ftdi.h",
  ]
  deps = [
    "//sdk/banjo/fuchsia.hardware.usb:fuchsia.hardware.usb_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.usb.composite:fuchsia.hardware.usb.composite_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.i2c.businfo:fuchsia.hardware.i2c.businfo_cpp",
    "//sdk/lib/stdcompat",
    "//src/devices/lib/driver",
    "//src/devices/lib/driver:driver_runtime",
    "//src/lib/ddk",
    "//src/lib/ddk:ddk-metadata-headers",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/sync",
  ]
  public_deps = [
    "//sdk/banjo/fuchsia.hardware.usb.request:fuchsia.hardware.usb.request_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.ftdi:fuchsia.hardware.ftdi_cpp",
    "//sdk/fidl/fuchsia.hardware.i2cimpl:fuchsia.hardware.i2cimpl_cpp",
    "//sdk/fidl/fuchsia.hardware.serialimpl:fuchsia.hardware.serialimpl_cpp",
    "//sdk/lib/driver/outgoing/cpp",
    "//src/devices/usb/lib/usb",
    "//src/lib/ddktl",
    "//zircon/system/ulib/zx",
  ]

  # TODO(https://fxbug.dev/42176699): This target uses mutable tables which are deprecated,
  # rather than builders.
  configs += [ "//build/cpp:fidl-wire-deprecated-mutable-tables" ]
}

fuchsia_cc_driver("ftdi-driver") {
  legacy = true
  output_name = "ftdi"
  deps = [
    ":common",
    ":ftdi_bind",
  ]
}

fuchsia_driver_component("ftdi") {
  visibility = [
    ":*",
    "//src/devices/serial:drivers",
    "//src/devices/serial/drivers/ftdi/test:*",
  ]
  info = "meta/ftdi-info.json"
  component_name = "ftdi"
  deps = [ ":ftdi-driver" ]
  manifest = "meta/ftdi.cml"
}

fuchsia_driver_package("package") {
  package_name = "ftdi"
  driver_components = [ ":ftdi" ]
}

driver_bind_rules("ftdi_function_bind") {
  rules = "meta/ftdi_function.bind"
  tests = "meta/ftdi_function_tests.json"
  deps = [ "//src/devices/bind/fuchsia.usb" ]
}

fuchsia_cc_driver("ftdi-fake-usb-driver") {
  legacy = true
  output_name = "ftdi-fake-usb"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [ "ftdi-function.cc" ]
  deps = [
    ":ftdi_function_bind",
    "//sdk/banjo/fuchsia.hardware.usb:fuchsia.hardware.usb_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.usb.function:fuchsia.hardware.usb.function_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.usb.request:fuchsia.hardware.usb.request_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.ftdi:fuchsia.hardware.ftdi_cpp",
    "//src/devices/lib/driver",
    "//src/devices/usb/lib/usb",
    "//src/devices/usb/lib/usb:hid",
    "//src/devices/usb/lib/usb:peripheral",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zircon-internal",
  ]
}

fuchsia_driver_component("ftdi-fake-usb") {
  info = "meta/ftdi-fake-usb-info.json"
  component_name = "ftdi-fake-usb"
  deps = [ ":ftdi-fake-usb-driver" ]
  manifest = "meta/ftdi-fake-usb.cml"
}

group("tests") {
  testonly = true
  deps = [
    ":ftdi_bind_test",
    ":ftdi_function_bind_test",
    "test:ftdi-i2c-test-package",
    "test:ftdi-integration-test",
  ]
}
