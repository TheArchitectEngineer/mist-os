# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("pwm-bind") {
  rules = "meta/pwm.bind"
  bind_output = "pwm.bindbc"
  tests = "meta/bind-tests.json"
  deps = [ "//src/devices/bind/fuchsia.pwm" ]
}

fuchsia_cc_driver("pwm-driver") {
  output_name = "pwm"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  deps = [ ":common" ]
}

fuchsia_driver_component("pwm") {
  visibility = [
    ":*",
    "//src/devices/pwm:drivers",
    "//src/devices/pwm/drivers:pwm",
  ]
  info = "meta/pwm-info.json"
  component_name = "pwm"
  deps = [
    ":pwm-bind",
    ":pwm-driver",
  ]
  manifest = "meta/pwm.cml"
}

fuchsia_driver_package("package") {
  package_name = "pwm"
  driver_components = [ ":pwm" ]
}

test("pwm-test-bin") {
  output_name = "pwm-test"
  sources = [ "pwm-test.cc" ]
  deps = [
    ":common",
    "//sdk/lib/driver/testing/cpp",
    "//src/lib/ddk:ddk_metadata",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
  ]
}

source_set("common") {
  sources = [
    "pwm.cc",
    "pwm.h",
  ]
  deps = [
    "//src/devices/bind/fuchsia:fuchsia_cpp",
    "//src/lib/ddk:ddk_metadata",
  ]
  public_deps = [
    "//sdk/banjo/fuchsia.hardware.pwm:fuchsia.hardware.pwm_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.pwm:fuchsia.hardware.pwm_cpp",
    "//sdk/fidl/fuchsia.hardware.vreg:fuchsia.hardware.vreg_cpp",
    "//sdk/lib/driver/compat/cpp",
    "//sdk/lib/driver/component/cpp",
    "//sdk/lib/driver/devfs/cpp",
    "//src/devices/lib/amlogic",
    "//zircon/system/ulib/zircon-internal",
  ]
}

fuchsia_unittest_package("pwm-test") {
  deps = [ ":pwm-test-bin" ]
  test_specs = {
    environments = basic_envs
  }
}

group("tests") {
  testonly = true
  deps = [
    ":pwm-bind_test",
    ":pwm-test",
  ]
}
