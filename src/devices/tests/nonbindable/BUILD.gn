# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

group("tests") {
  testonly = true
  deps = [ ":pkg" ]
}

group("drivers") {
  testonly = true
  deps = [ ":component" ]
}

driver_bind_rules("bind") {
  rules = "meta/nonbindable.bind"
  bind_output = "nonbindable.bindbc"
  deps = [
    "//src/devices/bind/fuchsia.pci",
    "//src/devices/bind/fuchsia.test",
  ]
}

fuchsia_cc_driver("driver") {
  legacy = true
  output_name = "test-nonbindable"
  sources = [ "nonbindable.cc" ]
  deps = [
    ":bind",
    "//src/devices/lib/driver",
    "//src/lib/ddktl",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_driver_component("component") {
  component_name = "test-nonbindable"
  deps = [ ":driver" ]
  info = "meta/component-info.json"
  manifest = "meta/component.cml"
}

test("nonbindable-test") {
  sources = [ "test.cc" ]
  deps = [
    ":component",
    "//sdk/fidl/fuchsia.driver.test:fuchsia.driver.test_cpp",
    "//sdk/fidl/fuchsia.io:fuchsia.io_cpp",
    "//sdk/lib/component/incoming/cpp",
    "//sdk/lib/device-watcher/cpp",
    "//sdk/lib/driver_test_realm:static",
    "//src/devices/misc/drivers/test-parent",
    "//src/lib/fxl/test:gtest_main",
  ]
}

fuchsia_unittest_package("pkg") {
  package_name = "nonbindable-test"
  deps = [ ":nonbindable-test" ]
}
