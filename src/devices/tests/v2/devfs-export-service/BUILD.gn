# Copyright 2025 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/drivers.gni")
import("//build/rust/rustc_test.gni")
import("//build/test.gni")
import("//build/testing/cc_test_executable.gni")

group("tests") {
  testonly = true
  deps = [
    ":devfs_services_test",
    ":devfs_services_test_cpp",
  ]
}

fidl("fuchsia.services.test") {
  testonly = true
  sources = [ "services.test.fidl" ]
}

driver_bind_rules("root_bind") {
  disable_autobind = true
  bind_output = "root.bindbc"
}

fuchsia_cc_driver("root_driver") {
  testonly = true
  output_name = "devfs_services_root"
  sources = [ "root_driver.cc" ]
  deps = [
    ":fuchsia.services.test_cpp",
    ":root_bind",
    "//sdk/fidl/fuchsia.component.decl:fuchsia.component.decl_cpp",
    "//sdk/lib/driver/component/cpp:cpp",
    "//sdk/lib/driver/devfs/cpp",
    "//src/devices/lib/driver:driver_runtime",
  ]
}

fuchsia_driver_component("root") {
  testonly = true
  manifest = "meta/root.cml"
  deps = [ ":root_driver" ]
  info = "meta/root-info.json"
}

rustc_test("bin") {
  edition = "2021"
  testonly = true
  output_name = "devfs_services_bin"
  source_root = "test.rs"
  sources = [ "test.rs" ]
  deps = [
    ":fuchsia.services.test_rust",
    "//sdk/fidl/fuchsia.driver.test:fuchsia.driver.test_rust",
    "//sdk/lib/driver_test_realm/realm_builder/rust",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-component-test",
    "//third_party/rust_crates:anyhow",
  ]
}

test("devfs_topological_test") {
  sources = [ "topo_test.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.device.fs:fuchsia.device.fs_cpp",
    "//sdk/fidl/fuchsia.driver.test:fuchsia.driver.test_cpp",
    "//sdk/lib/component/incoming/cpp",
    "//sdk/lib/device-watcher/cpp",
    "//sdk/lib/driver_test_realm/realm_builder/cpp",
    "//sdk/lib/syslog/cpp",
    "//src/devices/bind/fuchsia.platform:fuchsia.platform_cpp",
    "//src/devices/lib/client:device_topology",
    "//src/lib/fxl/test:gtest_main",
  ]
}
fuchsia_unittest_package("devfs_services_test_cpp") {
  deps = [
    ":devfs_topological_test",
    ":root",
    "//sdk/lib/driver_test_realm:driver_test_realm_component",
  ]
}

fuchsia_unittest_package("devfs_services_test") {
  deps = [
    ":bin",
    ":root",
    "//sdk/lib/driver_test_realm:driver_test_realm_component",
  ]
}
