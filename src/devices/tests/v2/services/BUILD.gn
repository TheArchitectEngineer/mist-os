# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/drivers.gni")
import("//build/rust/rustc_test.gni")

group("tests") {
  testonly = true
  deps = [ ":services_test" ]
}

fidl("fuchsia.services.test") {
  testonly = true
  sources = [ "services.test.fidl" ]
}

driver_bind_rules("root_bind") {
  disable_autobind = true
  bind_output = "root.bindbc"
}

fuchsia_cc_driver("root_driver") {
  testonly = true
  output_name = "services_root"
  sources = [ "root_driver.cc" ]
  deps = [
    ":fuchsia.services.test_cpp",
    ":root_bind",
    "//sdk/fidl/fuchsia.component.decl:fuchsia.component.decl_cpp",
    "//sdk/lib/driver/component/cpp:cpp",
    "//src/devices/lib/driver:driver_runtime",
  ]
}

driver_bind_rules("v1_driver_bind") {
  rules = "meta/v1_driver.bind"
  bind_output = "v1_driver.bindbc"
  deps = [ "//src/devices/bind/fuchsia.test" ]
}

fuchsia_cc_driver("v1_driver") {
  testonly = true
  legacy = true
  output_name = "v1_driver"
  sources = [ "v1_driver.cc" ]
  deps = [
    ":fuchsia.services.test_cpp",
    ":v1_driver_bind",
    "//src/devices/lib/driver",
    "//src/devices/lib/driver:driver_runtime",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/inspect",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_driver_component("v1_driver_component") {
  testonly = true
  component_name = "v1_driver"
  deps = [ ":v1_driver" ]
  info = "meta/v1-driver-info.json"
  manifest = "meta/v1_driver.cml"
}

fuchsia_driver_component("root") {
  testonly = true
  manifest = "meta/root.cml"
  deps = [ ":root_driver" ]
  info = "meta/root-info.json"
}

rustc_test("bin") {
  edition = "2021"
  testonly = true
  output_name = "services_bin"
  source_root = "test.rs"
  sources = [ "test.rs" ]
  deps = [
    ":fuchsia.services.test_rust",
    "//sdk/fidl/fuchsia.driver.test:fuchsia.driver.test_rust",
    "//sdk/lib/driver_test_realm/realm_builder/rust",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-component-test",
    "//third_party/rust_crates:anyhow",
  ]
}

fuchsia_unittest_package("services_test") {
  deps = [
    ":bin",
    ":root",
    ":v1_driver_component",
    "//sdk/lib/driver_test_realm:driver_test_realm_component",
  ]
}
