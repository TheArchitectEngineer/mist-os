# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("i2c_bind") {
  rules = "meta/i2c.bind"
  bind_output = "i2c.bindbc"
  tests = "meta/tests.json"
  deps =
      [ "//sdk/fidl/fuchsia.hardware.i2cimpl:fuchsia.hardware.i2cimpl_bindlib" ]
}

source_set("common") {
  sources = [
    "i2c-child-server.cc",
    "i2c-child-server.h",
    "i2c.cc",
    "i2c.h",
  ]
  deps = [
    ":i2c_bind",
    "//sdk/fidl/fuchsia.hardware.i2c:fuchsia.hardware.i2c_cpp",
    "//sdk/fidl/fuchsia.scheduler:fuchsia.scheduler_cpp",
    "//sdk/lib/driver/compat/cpp",
    "//src/devices/bind/fuchsia:fuchsia_cpp",
    "//src/devices/bind/fuchsia.i2c:fuchsia.i2c_cpp",
    "//zircon/system/ulib/trace",
    "//zircon/system/ulib/zx",
  ]
  public_deps = [
    "//sdk/fidl/fuchsia.hardware.i2c:fuchsia.hardware.i2c_cpp",
    "//sdk/fidl/fuchsia.hardware.i2c.businfo:fuchsia.hardware.i2c.businfo_cpp",
    "//sdk/fidl/fuchsia.hardware.i2cimpl:fuchsia.hardware.i2cimpl_cpp",
    "//sdk/lib/driver/compat/cpp",
    "//sdk/lib/driver/component/cpp",
    "//sdk/lib/driver/devfs/cpp",
    "//sdk/lib/driver/metadata/cpp",
  ]
}

fuchsia_cc_driver("i2c-driver") {
  output_name = "i2c"
  deps = [ ":common" ]
}

fuchsia_driver_component("i2c") {
  visibility = [
    ":*",
    "//src/devices/i2c:drivers",
  ]
  component_name = "i2c"
  deps = [ ":i2c-driver" ]
  info = "meta/i2c-info.json"
  manifest = "meta/i2c.cml"
}

fuchsia_driver_package("package") {
  package_name = "i2c"
  driver_components = [ ":i2c" ]
}

test("i2c-test-bin") {
  output_name = "i2c-test"
  configs += [ "//build/config:all_source" ]
  sources = [
    "fake-i2c-impl.cc",
    "fake-i2c-impl.h",
    "i2c-test-env.h",
    "i2c-test.cc",
    "i2c-transaction-test.cc",
  ]
  deps = [
    ":common",
    "//sdk/fidl/fuchsia.scheduler:fuchsia.scheduler_cpp",
    "//sdk/lib/driver/metadata/cpp",
    "//sdk/lib/driver/testing/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
  ]
}

fuchsia_unittest_package("i2c-test") {
  deps = [ ":i2c-test-bin" ]
  test_specs = {
    environments = basic_envs
    log_settings = {
      # The `BadTransfer` test purposefully triggers an error log.
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [ ":i2c-test" ]
}
