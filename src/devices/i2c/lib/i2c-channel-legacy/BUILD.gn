# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")

source_set("i2c-channel-legacy") {
  sources = [ "i2c-channel.h" ]
  public_deps = [
    # <lib/i2c-channel-legacy/i2c-channel.h> has
    # #includes <fidl/fuchsia.hardware.i2c/cpp/wire.h>
    "//sdk/fidl/fuchsia.hardware.i2c:fuchsia.hardware.i2c_cpp",

    # <lib/i2c-channel-legacy/i2c-channel.h> has #include <zircon/types.h>
    "//zircon/system/ulib/zx",

    # <lib/i2c-channel-legacy/i2c-channel.h> has #include <lib/ddk/driver.h>
    "//src/lib/ddk",
  ]
  deps = [ "//zircon/system/ulib/zx" ]
}

test("i2c-channel-legacy-test-bin") {
  output_name = "i2c-channel-legacy-test"
  sources = [ "i2c-channel-legacy-test.cc" ]
  deps = [
    ":i2c-channel-legacy",
    "//sdk/fidl/fuchsia.hardware.i2c:fuchsia.hardware.i2c_cpp",
    "//sdk/lib/async-loop:async-loop-cpp",
    "//sdk/lib/async_patterns/testing/cpp",
    "//sdk/lib/component/outgoing/cpp",
    "//src/devices/i2c/testing/fake-i2c",
    "//src/devices/testing/mock-ddk",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("i2c-channel-legacy-test") {
  deps = [ ":i2c-channel-legacy-test-bin" ]
}

group("tests") {
  testonly = true
  deps = [ ":i2c-channel-legacy-test" ]
}
