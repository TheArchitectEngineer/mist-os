# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("spi_bind") {
  rules = "meta/spi.bind"
  bind_output = "spi.bindbc"
  tests = "meta/tests.json"
  deps =
      [ "//sdk/fidl/fuchsia.hardware.spiimpl:fuchsia.hardware.spiimpl_bindlib" ]
}

group("tests") {
  testonly = true
  deps = [
    ":spi-test",
    ":spi_bind_test",
  ]
}

source_set("common") {
  sources = [
    "spi-child.cc",
    "spi-child.h",
    "spi.cc",
    "spi.h",
  ]
  deps = [
    ":spi_bind",
    "//sdk/fidl/fuchsia.scheduler:fuchsia.scheduler_cpp",
    "//sdk/lib/async",
    "//sdk/lib/component/outgoing/cpp",
    "//sdk/lib/fit",
    "//src/devices/bind/fuchsia:fuchsia_cpp",
    "//src/lib/ddk:ddk_metadata",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/trace",
    "//zircon/system/ulib/zx",
  ]
  public_deps = [
    "//sdk/fidl/fuchsia.hardware.spi:fuchsia.hardware.spi_bindlib_cpp",
    "//sdk/fidl/fuchsia.hardware.spi:fuchsia.hardware.spi_cpp",
    "//sdk/fidl/fuchsia.hardware.spi.businfo:fuchsia.hardware.spi.businfo_cpp",
    "//sdk/fidl/fuchsia.hardware.spiimpl:fuchsia.hardware.spiimpl_cpp",
    "//sdk/lib/driver/compat/cpp",
    "//sdk/lib/driver/devfs/cpp",
    "//sdk/lib/driver/runtime:driver_runtime_cpp",
  ]
}

fuchsia_cc_driver("spi-driver") {
  output_name = "spi"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  deps = [ ":common" ]
}

fuchsia_driver_component("spi") {
  visibility = [
    ":*",
    "//src/devices/bus/drivers/platform/test:*",
    "//src/devices/spi:drivers",
  ]
  info = "meta/spi-info.json"
  component_name = "spi"
  manifest = "meta/spi.cml"
  deps = [ ":spi-driver" ]
}

fuchsia_driver_package("package") {
  package_name = "spi"
  driver_components = [ ":spi" ]
}

test("spi-test-bin") {
  output_name = "spi-test"
  sources = [ "spi-test.cc" ]
  deps = [
    ":common",
    "//sdk/fidl/fuchsia.scheduler:fuchsia.scheduler_cpp",
    "//sdk/lib/driver/metadata/cpp",
    "//sdk/lib/driver/testing/cpp",
    "//src/lib/ddk:ddk_metadata",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/spi",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zircon-internal",
  ]
}

fuchsia_unittest_package("spi-test") {
  deps = [ ":spi-test-bin" ]
  test_specs = {
    environments = basic_envs
    log_settings = {
      max_severity = "ERROR"
    }
  }
}
