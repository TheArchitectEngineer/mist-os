# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/assembly/bootfs_files_for_assembly.gni")
import("//build/components.gni")
import("//build/test.gni")

executable("suspendctl") {
  visibility = [ ":*" ]
  sources = [
    "main.cc",
    "suspendctl.cc",
  ]
  deps = [
    "//sdk/fidl/fuchsia.device:fuchsia.device_cpp",
    "//sdk/fidl/fuchsia.hardware.power.suspend:fuchsia.hardware.power.suspend_cpp",
    "//sdk/lib/component/incoming/cpp",
    "//sdk/lib/fit",
    "//zircon/system/ulib/zx",
  ]
}

bootfs_files_for_assembly("bootfs") {
  deps = [ ":suspendctl" ]
}

test("suspendctl-test-bin") {
  output_name = "suspendctl-test"
  sources = [
    "suspendctl-test.cc",
    "suspendctl.cc",
  ]
  include_dirs = [ "." ]
  deps = [
    "//sdk/fidl/fuchsia.device:fuchsia.device_cpp_testing",
    "//sdk/fidl/fuchsia.hardware.power.suspend:fuchsia.hardware.power.suspend_cpp_testing",
    "//sdk/lib/async-loop:async-loop-cpp",
    "//sdk/lib/component/incoming/cpp",
    "//sdk/lib/fidl/cpp/wire",
    "//src/lib/fxl/test:gtest_main",
  ]
}

fuchsia_unittest_package("suspendctl-test") {
  deps = [ ":suspendctl-test-bin" ]
  test_specs = {
    environments = basic_envs
  }
}

group("tests") {
  testonly = true
  deps = [ ":suspendctl-test" ]
}
