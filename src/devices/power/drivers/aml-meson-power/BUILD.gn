# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("aml-meson-power-bind") {
  rules = "meta/aml-power.bind"
  bind_output = "aml-meson-power.bindbc"
  tests = "meta/bind-tests.json"
  deps = [
    "//sdk/fidl/fuchsia.hardware.pwm:fuchsia.hardware.pwm_bindlib",
    "//sdk/fidl/fuchsia.hardware.vreg:fuchsia.hardware.vreg_bindlib",
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.amlogic.platform.a311d",
    "//src/devices/bind/fuchsia.devicetree",
    "//src/devices/bind/fuchsia.google.platform",
    "//src/devices/bind/fuchsia.regulator",
  ]
}

source_set("common") {
  sources = [
    "aml-power.cc",
    "aml-power.h",
  ]
  public_deps = [
    "//sdk/banjo/fuchsia.hardware.powerimpl:fuchsia.hardware.powerimpl_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.power:fuchsia.hardware.power_cpp",
    "//sdk/fidl/fuchsia.hardware.pwm:fuchsia.hardware.pwm_cpp",
    "//sdk/fidl/fuchsia.hardware.vreg:fuchsia.hardware.vreg_cpp",
    "//sdk/lib/driver/compat/cpp",
    "//sdk/lib/driver/component/cpp",
    "//src/devices/lib/amlogic",
    "//zircon/system/ulib/zx",
  ]
  deps = [
    "//src/lib/ddk:ddk_metadata",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/hwreg",
    "//zircon/system/ulib/sync",
  ]
}

fuchsia_cc_driver("aml-meson-power-driver") {
  output_name = "aml-meson-power"
  defines = [ "_ALL_SOURCE" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  deps = [
    ":aml-meson-power-bind",
    ":common",
  ]
}

fuchsia_driver_component("aml-meson-power") {
  info = "meta/aml-meson-power-info.json"
  component_name = "aml-meson-power"
  deps = [ ":aml-meson-power-driver" ]
  manifest = "meta/aml-meson-power.cml"
}

fuchsia_driver_package("package") {
  package_name = "aml-meson-power"
  export_to_bazel = true
  driver_components = [ ":aml-meson-power" ]
}

test("aml-meson-power-test-bin") {
  output_name = "aml-meson-power-test"
  sources = [ "aml-power-test.cc" ]
  deps = [
    ":aml-meson-power-bind",
    ":common",
    "//sdk/fidl/fuchsia.hardware.pwm:fuchsia.hardware.pwm_cpp_testing",
    "//sdk/fidl/fuchsia.hardware.vreg:fuchsia.hardware.vreg_cpp_testing",
    "//sdk/lib/driver/testing/cpp",
    "//src/devices/bind/fuchsia.amlogic.platform:fuchsia.amlogic.platform_cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
  ]
}

fuchsia_unittest_package("aml-meson-power-test") {
  deps = [ ":aml-meson-power-test-bin" ]
  test_specs = {
    environments = basic_envs
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [
    ":aml-meson-power-bind_test",
    ":aml-meson-power-test",
  ]
}
