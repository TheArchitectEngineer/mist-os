# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("aml-pwm-regulator-bind") {
  rules = "meta/aml-pwm-regulator.bind"
  tests = "meta/bind-tests.json"
  deps = [
    "//sdk/fidl/fuchsia.hardware.pwm:fuchsia.hardware.pwm_bindlib",
    "//src/devices/bind/fuchsia.devicetree",
    "//src/devices/bind/fuchsia.gpio",
    "//src/devices/bind/fuchsia.platform",
    "//src/devices/bind/fuchsia.pwm",
  ]
}

source_set("common") {
  sources = [
    "aml-pwm-regulator.cc",
    "aml-pwm-regulator.h",
  ]
  deps = [ "//sdk/lib/driver/platform-device/cpp" ]
  public_deps = [
    ":aml-pwm-regulator-bind",
    "//sdk/fidl/fuchsia.hardware.pwm:fuchsia.hardware.pwm_cpp",
    "//sdk/fidl/fuchsia.hardware.vreg:fuchsia.hardware.vreg_cpp",
    "//sdk/lib/driver/component/cpp:cpp",
    "//src/devices/bind/fuchsia:fuchsia_cpp",
    "//src/devices/bind/fuchsia.regulator:fuchsia.regulator_cpp",
    "//src/devices/lib/amlogic",
    "//src/devices/lib/driver:driver_runtime",
    "//zircon/system/ulib/fbl",
  ]
}

fuchsia_cc_driver("aml-pwm-regulator-driver") {
  output_name = "aml-pwm-regulator"
  deps = [ ":common" ]
}

fuchsia_driver_component("aml-pwm-regulator") {
  info = "meta/aml-pwm-regulator-info.json"
  component_name = "aml-pwm-regulator"
  deps = [ ":aml-pwm-regulator-driver" ]
  manifest = "meta/aml-pwm-regulator.cml"
}

fuchsia_driver_package("package") {
  package_name = "aml-pwm-regulator"
  driver_components = [ ":aml-pwm-regulator" ]
}

test("aml-pwm-regulator-test-bin") {
  output_name = "aml-pwm-regulator-test"
  sources = [ "aml-pwm-regulator-test.cc" ]
  deps = [
    ":common",
    "//sdk/fidl/fuchsia.hardware.pwm:fuchsia.hardware.pwm_cpp_testing",
    "//sdk/lib/driver/fake-platform-device/cpp",
    "//sdk/lib/driver/testing/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
  ]

  # TODO(https://fxbug.dev/42176699): This target uses mutable tables which are deprecated,
  # rather than builders.
  configs += [ "//build/cpp:fidl-wire-deprecated-mutable-tables" ]
}

fuchsia_unittest_package("aml-pwm-regulator-test") {
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
  deps = [ ":aml-pwm-regulator-test-bin" ]
}

group("tests") {
  testonly = true
  deps = [
    ":aml-pwm-regulator-bind_test",
    ":aml-pwm-regulator-test",
  ]
}
