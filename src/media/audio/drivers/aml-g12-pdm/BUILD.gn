# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

assert(target_cpu == "arm64", "The aml-g12-pdm driver is only valid on arm64.")

import("//build/bind/bind.gni")
import("//build/drivers.gni")

driver_bind_rules("aml_g12_pdm_bind") {
  rules = "meta/aml_g12_pdm.bind"
  bind_output = "aml-g12-pdm.bindbc"
  tests = "meta/tests.json"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.clock",
    "//src/devices/bind/fuchsia.gpio",
    "//src/devices/bind/fuchsia.platform",
  ]
}

fuchsia_cc_driver("aml-g12-pdm-driver") {
  legacy = true
  output_name = "aml-g12-pdm"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [ "audio-stream-in.cc" ]

  deps = [
    ":aml_g12_pdm_bind",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/lib/amlogic",
    "//src/devices/lib/driver",
    "//src/devices/lib/mmio",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//src/lib/memory_barriers",
    "//src/media/audio/drivers/lib/audio-driver-proto",
    "//src/media/audio/drivers/lib/audio-proto-utils",
    "//src/media/audio/lib/simple-audio-stream",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/inspect",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zx",
  ]

  # TODO(https://fxbug.dev/42176699): This target uses mutable tables which are deprecated,
  # rather than builders.
  configs += [ "//build/cpp:fidl-wire-deprecated-mutable-tables" ]
}

fuchsia_driver_component("aml-g12-pdm") {
  component_name = "aml-g12-pdm"
  deps = [ ":aml-g12-pdm-driver" ]
  info = "meta/aml-g12-pdm-info.json"
  manifest = "meta/aml-g12-pdm.cml"
}

fuchsia_driver_package("package") {
  package_name = "aml-g12-pdm"
  export_to_bazel = true
  driver_components = [ ":aml-g12-pdm" ]
}

group("tests") {
  testonly = true
  deps = [
    ":aml_g12_pdm_bind_test",
    "test:test-package",
  ]
}
