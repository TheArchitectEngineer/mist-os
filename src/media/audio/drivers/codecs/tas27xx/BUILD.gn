# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("ti_tas27xx-bind") {
  rules = "meta/ti_tas27xx.bind"
  bind_output = "tas27xx.bindbc"
  tests = "meta/tests.json"
  deps = [
    "//sdk/fidl/fuchsia.hardware.gpio:fuchsia.hardware.gpio_bindlib",
    "//sdk/fidl/fuchsia.hardware.i2c:fuchsia.hardware.i2c_bindlib",
    "//src/devices/bind/fuchsia.gpio",
    "//src/devices/bind/fuchsia.i2c",
    "//src/devices/bind/fuchsia.platform",
    "//src/devices/bind/fuchsia.ti.platform",
  ]
}

fuchsia_cc_driver("tas27xx-driver") {
  legacy = true
  output_name = "tas27xx"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [ "tas27xx.cc" ]
  deps = [
    ":ti_tas27xx-bind",
    "//sdk/fidl/fuchsia.hardware.gpio:fuchsia.hardware.gpio_cpp",
    "//sdk/lib/async:async-cpp",
    "//src/devices/i2c/lib/device-protocol-i2c-channel",
    "//src/devices/lib/driver",
    "//src/devices/lib/ti",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//src/media/audio/lib/simple-codec",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zx",
  ]

  # TODO(https://fxbug.dev/42136089): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_driver_component("tas27xx") {
  component_name = "tas27xx"
  deps = [ ":tas27xx-driver" ]
  info = "meta/tas27xx-info.json"
  manifest = "meta/tas27xx.cml"
}

fuchsia_driver_package("package") {
  package_name = "tas27xx"
  export_to_bazel = true
  driver_components = [ ":tas27xx" ]
}

test("tas27xx-test-bin") {
  configs += [ "//build/config:all_source" ]
  output_name = "tas27xx-test"
  sources = [
    "tas27xx-test.cc",
    "tas27xx.cc",
  ]
  deps = [
    ":tas27xx",
    ":ti_tas27xx-bind",
    "//sdk/fidl/fuchsia.hardware.gpio:fuchsia.hardware.gpio_cpp",
    "//sdk/fidl/fuchsia.hardware.i2c:fuchsia.hardware.i2c_cpp",
    "//sdk/lib/async:async-cpp",
    "//sdk/lib/async-loop:async-loop-cpp",
    "//sdk/lib/async-loop:async-loop-default",
    "//sdk/lib/async_patterns/testing/cpp",
    "//sdk/lib/inspect/testing/cpp",
    "//src/devices/gpio/testing/fake-gpio",
    "//src/devices/i2c/lib/device-protocol-i2c-channel",
    "//src/devices/i2c/testing/mock-i2c:mock-i2c-gtest",
    "//src/devices/lib/ti",
    "//src/devices/testing/mock-ddk",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
    "//src/media/audio/lib/simple-codec",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zx",
  ]

  # TODO(https://fxbug.dev/42136089): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_unittest_package("tas27xx-test-package") {
  package_name = "tas27xx-test"
  deps = [ ":tas27xx-test-bin" ]
  test_specs = {
    environments = basic_envs
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [
    ":tas27xx-test-package",
    ":ti_tas27xx-bind_test",
  ]
}
