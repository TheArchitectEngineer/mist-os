# Copyright 2025 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")
import("//build/zbi/kernel_cmdline.gni")

driver_bind_rules("virtual-audio-bind") {
  rules = "meta/virtual-audio.bind"
  tests = "meta/bind-tests.json"
  deps = [ "//src/devices/bind/fuchsia.platform" ]
}

group("tests") {
  testonly = true
  deps = [ ":virtual-audio-test" ]
}

source_set("common") {
  sources = [
    "virtual-audio-composite.cc",
    "virtual-audio-composite.h",
    "virtual-audio.cc",
    "virtual-audio.h",
  ]
  deps = [
    "//src/devices/lib/driver:driver_runtime",
    "//src/media/audio/drivers/lib/audio-proto-utils",
    "//zircon/system/ulib/fbl",
  ]
  public_deps = [
    "//sdk/fidl/fuchsia.virtualaudio:fuchsia.virtualaudio_cpp",
    "//sdk/lib/driver/component/cpp",
    "//sdk/lib/driver/devfs/cpp",
    "//zircon/system/ulib/fzl",
  ]
}

fuchsia_cc_driver("virtual-audio-driver") {
  deps = [ ":common" ]
}

fuchsia_driver_component("virtual-audio-driver-component") {
  component_name = "virtual-audio-driver"
  deps = [
    ":virtual-audio-bind",
    ":virtual-audio-driver",
  ]
  info = "meta/virtual-audio-info.json"
  manifest = "meta/virtual-audio-driver.cml"
}

fuchsia_driver_package("virtual-audio") {
  driver_components = [ ":virtual-audio-driver-component" ]
}

test("virtual-audio-test-bin") {
  sources = [ "virtual-audio-test.cc" ]
  deps = [
    ":common",
    "//sdk/lib/driver/testing/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
  ]
}

fuchsia_unittest_package("virtual-audio-test") {
  deps = [ ":virtual-audio-test-bin" ]
  test_specs = {
    environments = basic_envs
    log_settings = {
      max_severity = "ERROR"
    }
  }
}
