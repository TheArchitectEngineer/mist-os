# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/zbi/kernel_cmdline.gni")

fuchsia_driver_package("virtual-audio-legacy") {
  driver_components = [ ":virtual-audio-legacy-driver-component" ]
}

driver_bind_rules("virtual-audio-legacy-bind") {
  rules = "meta/virtual-audio-legacy.bind"
  tests = "meta/bind-tests.json"
  deps = [ "//src/devices/bind/fuchsia.platform" ]
}

group("tests") {
  testonly = true
  deps = [ ":virtual-audio-legacy-bind_test" ]
}

fuchsia_cc_driver("virtual-audio-legacy-driver") {
  legacy = true

  sources = [
    "virtual-audio-codec.cc",
    "virtual-audio-codec.h",
    "virtual-audio-dai.cc",
    "virtual-audio-dai.h",
    "virtual-audio-device.cc",
    "virtual-audio-device.h",
    "virtual-audio-legacy.cc",
    "virtual-audio-legacy.h",
    "virtual-audio-stream.cc",
    "virtual-audio-stream.h",
  ]

  deps = [
    ":virtual-audio-legacy-bind",
    "//sdk/fidl/fuchsia.hardware.audio:fuchsia.hardware.audio_cpp",
    "//sdk/fidl/fuchsia.virtualaudio:fuchsia.virtualaudio_cpp",
    "//sdk/lib/async:async-cpp",
    "//sdk/lib/async-loop:async-loop-default",
    "//sdk/lib/driver/runtime:driver_runtime_cpp",
    "//src/devices/lib/driver",
    "//src/devices/lib/driver:driver_runtime",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//src/media/audio/drivers/lib/audio-driver-proto",
    "//src/media/audio/drivers/lib/audio-proto-utils",
    "//src/media/audio/lib/simple-audio-stream",
    "//zircon/system/ulib/affine",
    "//zircon/system/ulib/fzl",
  ]
}

fuchsia_driver_component("virtual-audio-legacy-driver-component") {
  component_name = "virtual-audio-legacy-driver"
  deps = [ ":virtual-audio-legacy-driver" ]
  info = "meta/virtual-audio-legacy-info.json"
  manifest = "meta/virtual-audio-legacy-driver.cml"
}

kernel_cmdline("kernel-logging") {
  args = [ "driver.virtual-audio-legacy.log=trace" ]
}
