# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")

source_set("src") {
  visibility = [ ":*" ]
  sources = [
    "generated-source.cc",
    "generated-source.h",
    "noise-source.h",
    "sine-source.h",
    "wav-common.cc",
    "wav-common.h",
    "wav-sink.cc",
    "wav-sink.h",
    "wav-source.cc",
    "wav-source.h",
  ]
  public_deps = [
    "//sdk/lib/async:async-cpp",
    "//sdk/lib/async-loop:async-loop-cpp",
    "//sdk/lib/async-loop:async-loop-default",
    "//src/lib/fsl",
    "//src/media/audio/drivers/lib/audio-proto-utils",
    "//src/media/audio/drivers/lib/audio-utils",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zx",
  ]
  deps = [ "//sdk/lib/fdio" ]
}

executable("bin") {
  output_name = "audio-driver-ctl"
  sources = [ "audio.cc" ]
  deps = [ ":src" ]
}

test("audio-driver-ctl-test") {
  output_name = "audio-driver-ctl-test"
  sources = [
    "noise-source-test.cc",
    "sine-source-test.cc",
  ]
  include_dirs = [ "." ]
  deps = [
    ":src",
    "//sdk/lib/fdio",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_package("audio-driver-ctl") {
  deps = [ ":bin" ]
}

fuchsia_unittest_package("audio-driver-ctl-test-pkg") {
  deps = [ ":audio-driver-ctl-test" ]
}

group("tests") {
  testonly = true
  deps = [ ":audio-driver-ctl-test-pkg" ]
}
