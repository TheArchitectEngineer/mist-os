# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/assembly/assembled_system.gni")
import("//build/assembly/product_assembly_configuration.gni")
import("//build/testing/host_test_data.gni")

if (is_fuchsia) {
  product_assembly_configuration("assembly_product_config") {
    testonly = true
    platform = {
      feature_set_level = "bootstrap"
      build_type = "eng"
      development_support = {
        include_netsvc = true
      }
      kernel = {
        oom_behavior = "job_kill"
      }
      storage = {
        filesystems = {
          image_name = "limited_netsvc_assembled_system"
          image_mode = "no_image"
        }
      }
    }
  }

  assembled_system("limited_netsvc_assembled_system") {
    testonly = true
    product_assembly_config_label = ":assembly_product_config"
    board_config_label = board_configuration_label
    generate_vbmeta = use_vbmeta
  }
}

if (is_host) {
  host_test_data("host_test_data") {
    _assembled_system_label =
        ":limited_netsvc_assembled_system($default_toolchain)"
    _out_dir = get_label_info(_assembled_system_label, "target_out_dir")
    sources = [ "${_out_dir}/limited_netsvc_assembled_system/limited_netsvc_assembled_system.zbi" ]
    deps = [ _assembled_system_label ]
  }
}
