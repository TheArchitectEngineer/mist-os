# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")

group("tests") {
  testonly = true
  public_deps = [
    ":fake-display-coordinator-connector-integration-tests-package",
    ":fake-display-coordinator-connector-unittests-package",
  ]
}

test("fake-display-coordinator-connector-unittests") {
  sources = [ "fake-display-coordinator-connector-unittest.cc" ]
  deps = [
    "//sdk/lib/fidl/hlcpp",
    "//src/graphics/display/testing/fake-coordinator-connector:service",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//third_party/googletest:gtest",
  ]
}

fuchsia_unittest_package(
    "fake-display-coordinator-connector-unittests-package") {
  package_name = "fake-display-coordinator-connector-unittests"

  manifest = "meta/fake-display-coordinator-connector-unittests.cml"
  deps = [ ":fake-display-coordinator-connector-unittests" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }

  # Needed to access the system Sysmem service.
  test_type = "vulkan"
}

test("fake-display-coordinator-connector-integration-tests") {
  sources = [ "fake-display-coordinator-connector-integration-test.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.hardware.display:fuchsia.hardware.display_cpp",
    "//sdk/lib/component/incoming/cpp",
    "//sdk/lib/device-watcher/cpp",
    "//sdk/lib/fidl/hlcpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
    "//third_party/googletest:gtest",
  ]
}

fuchsia_test_component(
    "fake-display-coordinator-connector-integration-tests-component") {
  component_name = "fake-display-coordinator-connector-integration-tests"

  manifest = "meta/fake-display-coordinator-connector-integration-tests.cml"
  deps = [ ":fake-display-coordinator-connector-integration-tests" ]

  # Needed to access the system Sysmem service.
  test_type = "vulkan"
}

fuchsia_test_package(
    "fake-display-coordinator-connector-integration-tests-package") {
  package_name = "fake-display-coordinator-connector-integration-test"

  test_components =
      [ ":fake-display-coordinator-connector-integration-tests-component" ]
  deps = [
    "//src/graphics/display/testing/fake-coordinator-connector:child-component",
  ]

  # TODO(https://fxbug.dev/399886375): Remove the error expectations after the
  # racy error logging on VSync is fixed.
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}
