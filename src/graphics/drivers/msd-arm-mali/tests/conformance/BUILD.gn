# Copyright 2025 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//src/graphics/lib/magma/gnbuild/magma.gni")

fuchsia_component("vendor-helper-arm-component") {
  testonly = true
  component_name = "vendor-helper"
  manifest = "meta/vendor-helper.cml"
  deps = [ ":magma-conformance-vendor-helper-arm" ]
}

fuchsia_test_component("magma-conformance-tests-arm-component") {
  component_name = "magma-conformance-tests"
  manifest = "meta/magma-conformance-tests-arm.cml"
  test_type = "system"
}

fuchsia_test_package("conformance") {
  package_name = "magma-conformance-tests-arm"
  test_components = [ ":magma-conformance-tests-arm-component" ]
  subpackages =
      [ "//src/graphics/magma/tests/integration:magma-conformance-tests-child" ]
  deps = [ ":vendor-helper-arm-component" ]
  test_specs = {
    environments = magma_arm_gpu_envs
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

executable("magma-conformance-vendor-helper-arm") {
  testonly = true
  sources = [ "vendor_helper_arm.cc" ]
  deps = [
    "//sdk/lib/async-loop:async-loop-cpp",
    "//sdk/lib/component/outgoing/cpp",
    "//sdk/lib/syslog/cpp",
    "//src/graphics/drivers/msd-arm-mali/include:include",
    "//src/graphics/magma/tests/integration:vendor_helper_interface",
  ]
}
