# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_library.gni")

rustc_library("bt-hfp") {
  edition = "2021"
  with_unit_tests = true

  sources = [
    "src/a2dp.rs",
    "src/audio.rs",
    "src/audio/codec.rs",
    "src/audio/dai.rs",
    "src/audio/inband.rs",
    "src/call.rs",
    "src/call/direction.rs",
    "src/call/indicators.rs",
    "src/call/list.rs",
    "src/call/number.rs",
    "src/codec_id.rs",
    "src/lib.rs",
    "src/sco.rs",
    "src/sco/connector.rs",
    "src/sco/state.rs",
    "src/sco/test_utils.rs",
  ]

  deps = [
    "//sdk/fidl/fuchsia.audio.device:fuchsia.audio.device_rust",
    "//sdk/fidl/fuchsia.bluetooth:fuchsia.bluetooth_rust",
    "//sdk/fidl/fuchsia.bluetooth.bredr:fuchsia.bluetooth.bredr_rust",
    "//sdk/fidl/fuchsia.bluetooth.hfp:fuchsia.bluetooth.hfp_rust",
    "//sdk/fidl/fuchsia.bluetooth.internal.a2dp:fuchsia.bluetooth.internal.a2dp_rust",
    "//sdk/fidl/fuchsia.hardware.audio:fuchsia.hardware.audio_rust",
    "//sdk/fidl/fuchsia.media:fuchsia.media_rust",
    "//sdk/rust/zx",
    "//src/connectivity/bluetooth/lib/bt-a2dp",
    "//src/connectivity/bluetooth/lib/fuchsia-audio-codec",
    "//src/connectivity/bluetooth/lib/fuchsia-audio-dai",
    "//src/connectivity/bluetooth/lib/fuchsia-audio-device",
    "//src/connectivity/bluetooth/lib/fuchsia-bluetooth",
    "//src/connectivity/bluetooth/lib/packet-encoding",
    "//src/connectivity/bluetooth/lib/profile-client",
    "//src/connectivity/bluetooth/lib/vigil",
    "//src/lib/diagnostics/inspect/derive",
    "//src/lib/diagnostics/inspect/rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-sync",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
    "//third_party/rust_crates:thiserror",
  ]

  test_deps = [
    "//src/connectivity/bluetooth/lib/profile-client:test-profile-server",
    "//src/lib/diagnostics/testing/diagnostics-assertions/rust:diagnostics-assertions",
    "//src/lib/fuchsia",
    "//src/lib/testing/fixture",
    "//third_party/rust_crates:futures-test",
  ]

  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
}

rustc_test("bt_hfp_a2dp_controller_bin") {
  edition = "2021"
  output_name = "bt_hfp_a2dp_controller_tests"
  features = [ "test_a2dp_controller" ]
  source_root = "src/a2dp.rs"

  deps = [
    "//sdk/fidl/fuchsia.bluetooth.internal.a2dp:fuchsia.bluetooth.internal.a2dp_rust",
    "//sdk/rust/zx",
    "//src/connectivity/bluetooth/lib/fuchsia-bluetooth",
    "//src/lib/async-utils",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
  ]

  sources = [ "src/a2dp.rs" ]

  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
}

fuchsia_unittest_package("bt-hfp-a2dp-controller-tests") {
  deps = [ ":bt_hfp_a2dp_controller_bin" ]
}

fuchsia_test_component("test-component") {
  manifest = "meta/bt-hfp-tests.cml"
  deps = [ ":bt-hfp_test" ]
  test_type = "system"
}

fuchsia_test_package("bt-hfp-tests") {
  test_components = [ ":test-component" ]
  subpackages = [ "//src/media/codec:codec_factory_test" ]
}

group("tests") {
  testonly = true
  deps = [
    ":bt-hfp-a2dp-controller-tests",
    ":bt-hfp-tests",
  ]
}
