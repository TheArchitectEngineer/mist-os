# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_test.gni")

# All tests in this file currently run only on the host. If that changes,
# remove this.
assert(is_host)

rustc_test("netstack3_core_threading_test") {
  edition = "2024"
  source_root = "threading/lib.rs"
  sources = [
    "threading/device_socket.rs",
    "threading/lib.rs",
    "threading/sndbuf.rs",
    "threading/tcp.rs",
  ]

  deps = [
    "//src/connectivity/lib/ip-test-macro",
    "//src/connectivity/lib/net-declare",
    "//src/connectivity/lib/net-types",
    "//src/connectivity/lib/packet-formats",
    "//src/connectivity/network/netstack3/core:netstack3-core-loom",
    "//src/connectivity/network/netstack3/core/base:netstack3-base-loom",
    "//src/connectivity/network/netstack3/core/ip:netstack3-ip-loom",
    "//src/connectivity/network/netstack3/core/tcp:netstack3-tcp-loom",
    "//src/lib/network/packet",
    "//third_party/rust_crates:assert_matches",
    "//third_party/rust_crates:loom",
    "//third_party/rust_crates:test-case",
  ]

  configs -= [ "//build/config/rust/lints:allow_unused_results" ]

  # The loom crate documentation recommends compiling with optimizations since
  # the number of iterations can be large enough to make tests unreasonably
  # slow otherwise.
  configs += [ "//build/config:optimize_moderate" ]

  # Add thinlto config if lto variants are not used.
  if (!is_lto_variant) {
    configs += [ "//build/config/lto:thinlto" ]
  }
}

group("tests") {
  testonly = true
  deps = []

  # The threading tests take a long time to run, and exceed the capabilities of
  # Fuchsia's coverage builders in CI.
  if (!is_coverage) {
    deps += [ ":netstack3_core_threading_test" ]
  }
}
