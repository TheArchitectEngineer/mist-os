# Copyright 2025 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import(
    "//src/connectivity/network/netstack3/core/rustc_library_with_features.gni")

rustc_library_with_features("netstack3-trace") {
  version = "0.0.1"
  edition = "2021"
  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
  configs += [ "//src/connectivity/network/netstack3:netstack3_configs" ]
  check_cfgs = [ "test" ]
  sources = [
    "src/id.rs",
    "src/lib.rs",
  ]

  deps = []
  if (is_fuchsia) {
    deps += [ "//src/lib/trace/rust:trace" ]
  }

  feature_sets = [
    {
      features = []
      with_unit_tests = true
      deps +=
          [ "//src/connectivity/network/netstack3/core/sync:netstack3-sync" ]
    },
    {
      target_name = "netstack3-trace-instrumented"
      with_unit_tests = true
      deps += [ "//src/connectivity/network/netstack3/core/sync:netstack3-sync-instrumented" ]
    },
  ]
  if (is_host) {
    feature_sets += [
      {
        target_name = "netstack3-trace-loom"
        testonly = true
        configs += [ "//src/connectivity/network/netstack3/core:loom" ]
        deps += [
          "//src/connectivity/network/netstack3/core/sync:netstack3-sync-loom",
        ]
      },
    ]
  }
}
