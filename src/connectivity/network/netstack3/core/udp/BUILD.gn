# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_library.gni")
import(
    "//src/connectivity/network/netstack3/core/rustc_library_with_features.gni")

rustc_library_with_features("netstack3-udp") {
  edition = "2021"
  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
  check_cfgs = [ "test" ]
  sources = [
    "src/base.rs",
    "src/counters.rs",
    "src/lib.rs",
  ]
  deps = [
    "//src/connectivity/lib/net-types",
    "//src/connectivity/lib/packet-formats",
    "//src/connectivity/network/netstack3/core/lock-order",
    "//src/lib/fakealloc",
    "//src/lib/network/packet",
    "//third_party/rust_crates:derivative",
    "//third_party/rust_crates:either",
    "//third_party/rust_crates:log",
    "//third_party/rust_crates:smallvec",
    "//third_party/rust_crates:thiserror",
  ]

  configs += [ "//src/connectivity/network/netstack3:netstack3_configs" ]

  feature_sets = [
    {
      features = []
      deps += [
        "//src/connectivity/network/netstack3/core/base:netstack3-base",
        "//src/connectivity/network/netstack3/core/datagram:netstack3-datagram",
        "//src/connectivity/network/netstack3/core/filter:netstack3-filter",
        "//src/connectivity/network/netstack3/core/ip:netstack3-ip",
        "//src/connectivity/network/netstack3/core/trace:netstack3-trace",
      ]
    },

    {
      target_name = "netstack3-udp-testutils"
      testonly = true
      features = [
        "testutils",
        "instrumented",
      ]
      deps += [
        "//src/connectivity/network/netstack3/core/base:netstack3-base-testutils",
        "//src/connectivity/network/netstack3/core/datagram:netstack3-datagram-testutils",
        "//src/connectivity/network/netstack3/core/filter:netstack3-filter-testutils",
        "//src/connectivity/network/netstack3/core/ip:netstack3-ip-testutils",
        "//src/connectivity/network/netstack3/core/trace:netstack3-trace-instrumented",
      ]
    },
    {
      target_name = "netstack3-udp-benchmarks"
      testonly = true
      features = [ "testutils" ]
      deps += [
        "//src/connectivity/network/netstack3/core/base:netstack3-base-benchmarks",
        "//src/connectivity/network/netstack3/core/datagram:netstack3-datagram-benchmarks",
        "//src/connectivity/network/netstack3/core/filter:netstack3-filter-benchmarks",
        "//src/connectivity/network/netstack3/core/ip:netstack3-ip-benchmarks",
        "//src/connectivity/network/netstack3/core/trace:netstack3-trace",
      ]
      configs += [ "//src/connectivity/network/netstack3/core:benchmark" ]
    },
    {
      target_name = "netstack3-udp-instrumented"
      with_unit_tests = true
      features = [ "instrumented" ]
      non_test_deps = [
        "//src/connectivity/network/netstack3/core/base:netstack3-base-instrumented",
        "//src/connectivity/network/netstack3/core/datagram:netstack3-datagram-instrumented",
        "//src/connectivity/network/netstack3/core/filter:netstack3-filter-instrumented",
        "//src/connectivity/network/netstack3/core/ip:netstack3-ip-instrumented",
        "//src/connectivity/network/netstack3/core/trace:netstack3-trace-instrumented",
      ]
      test_deps = [
        "//src/connectivity/lib/ip-test-macro",
        "//src/connectivity/lib/net-declare",
        "//src/connectivity/network/netstack3/core/base:netstack3-base-testutils",
        "//src/connectivity/network/netstack3/core/datagram:netstack3-datagram-testutils",
        "//src/connectivity/network/netstack3/core/filter:netstack3-filter-testutils",
        "//src/connectivity/network/netstack3/core/ip:netstack3-ip-testutils",
        "//src/connectivity/network/netstack3/core/trace:netstack3-trace-instrumented",
        "//third_party/rust_crates:assert_matches",
        "//third_party/rust_crates:itertools",
        "//third_party/rust_crates:test-case",
      ]
    },
  ]
  if (is_host) {
    feature_sets += [
      {
        target_name = "netstack3-udp-loom"
        features = [
          "testutils",
          "instrumented",
        ]
        testonly = true
        deps += [
          "//src/connectivity/network/netstack3/core/base:netstack3-base-loom",
          "//src/connectivity/network/netstack3/core/datagram:netstack3-datagram-loom",
          "//src/connectivity/network/netstack3/core/filter:netstack3-filter-loom",
          "//src/connectivity/network/netstack3/core/ip:netstack3-ip-loom",
          "//src/connectivity/network/netstack3/core/trace:netstack3-trace-loom",
        ]
        configs += [ "//src/connectivity/network/netstack3/core:loom" ]
      },
    ]
  }
}

fuchsia_unittest_package("netstack3-core-udp-test") {
  deps = [ ":netstack3-udp-instrumented_test" ]
}

group("tests") {
  testonly = true
  deps = [
    ":netstack3-udp-instrumented_test($host_toolchain)",
    "tests",
  ]

  # TODO(https://fxbug.dev/360004388): These tests are causing ssh connect
  # error on core.arm64-coverage-qemu_kvm. Fix the issue and enable them back.
  if (!is_coverage || target_cpu != "arm64") {
    deps += [ ":netstack3-core-udp-test" ]
  }
}
