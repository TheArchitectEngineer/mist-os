# Copyright 2025 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_library.gni")
import("//build/rust/rustc_test.gni")

_common = {
  edition = "2024"
  sources = [ "src/lib.rs" ]
  check_cfgs = [
    "loom",
    "test",
  ]
}

rustc_library("oneshot-sync") {
  forward_variables_from(_common, "*")
  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
}

if (is_host) {
  # The tests only build on host because we use loom, which itself only can
  # build on host.
  rustc_test("oneshot-sync-tests") {
    forward_variables_from(_common, "*")
    configs -= [ "//build/config/rust/lints:allow_unused_results" ]
    deps = [ "//third_party/rust_crates:loom" ]
    rustflags = [ "--cfg=loom" ]
  }
}

group("tests") {
  testonly = true
  deps = [ ":oneshot-sync-tests($host_toolchain)" ]
}
