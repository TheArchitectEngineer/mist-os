# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

group("tests") {
  testonly = true

  # There aren't any tests but this makes sure it's in the build graph so the build doesn't break.
  deps = [ ":blackout-fxfs-tree" ]
}

rustc_binary("blackout-fxfs-tree-bin") {
  output_name = "blackout_fxfs_tree_bin"
  testonly = true
  edition = "2021"
  deps = [
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//sdk/rust/zx",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-fs",
    "//src/storage/blackout/target/lib:blackout-config",
    "//src/storage/blackout/target/lib:blackout-target",
    "//src/storage/fidl/fuchsia.fs.startup:fuchsia.fs.startup_rust",
    "//src/storage/fxfs/fidl/fuchsia.fxfs:fuchsia.fxfs_rust",
    "//src/storage/lib/fs_management/rust:fs_management",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:async-trait",
    "//third_party/rust_crates:log",
    "//third_party/rust_crates:rand",
  ]
  data_deps = [
    "//src/storage/crypt/fxfs",
    "//src/storage/fxfs",
  ]
  sources = [ "src/main.rs" ]
  rustenv = [ "DATA_FILESYSTEM_FORMAT=fxfs" ]
}

fuchsia_component("blackout-fxfs-tree-component") {
  testonly = true
  deps = [
    ":blackout-fxfs-tree-bin",
    "//src/storage/fs_test:test_crypt_service",
    "//src/storage/fxfs:fxfs_component",
    "//src/storage/fxfs:test_fxfs_component_manifest_shard",
  ]
  manifest = "meta/blackout_fxfs_tree.cml"
}

fuchsia_package("blackout-fxfs-tree") {
  testonly = true
  deps = [ ":blackout-fxfs-tree-component" ]
}
