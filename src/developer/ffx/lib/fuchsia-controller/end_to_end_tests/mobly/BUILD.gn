# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
import("//build/python/python_mobly_test.gni")
import("//build/testing/environments.gni")
import("//build/testing/host_test_data.gni")

group("fuchsia_controller_tests") {
  testonly = true
  deps = [
    ":echo_stress_test",
    ":nuc_target_identity_tests",
    ":reboot_test",
    ":tracing_test",
    ":vim3_target_identity_tests",
    ":x64_target_identity_tests",
  ]
}

group("fuchsia_controller_tests_staging") {
  testonly = true
  deps = []
}

if (is_host) {
  target_identity_deps = [
    "//src/developer/ffx/lib/fuchsia-controller/tests:core_sdk_host_test_data",
    "//src/developer/ffx/lib/fuchsia-controller/tests:fuchsia_controller_test_data",
  ]

  target_identity_libraries = [
    "//sdk/fidl/fuchsia.device:fuchsia.device_python",
    "//sdk/fidl/fuchsia.feedback:fuchsia.feedback_python",
    "//sdk/fidl/fuchsia.hardware.power.statecontrol:fuchsia.hardware.power.statecontrol_python",
    "//sdk/fidl/fuchsia.io:fuchsia.io_python",
    "//src/developer/ffx/fidl:fuchsia.developer.ffx_python",
  ]

  python_mobly_test("x64_target_identity_tests") {
    main_source = "target_identity_tests.py"
    libraries = [
      "//src/developer/ffx/lib/fuchsia-controller:fidl_bindings",
      "//src/developer/ffx/lib/fuchsia-controller/end_to_end_tests/mobly/mobly_controller",
    ]
    libraries += target_identity_libraries
    params_source = "params_x64.yaml"
    environments = [ emu_env ]
    deps = target_identity_deps
  }

  python_mobly_test("vim3_target_identity_tests") {
    main_source = "target_identity_tests.py"
    libraries = [
      "//src/developer/ffx/lib/fuchsia-controller:fidl_bindings",
      "//src/developer/ffx/lib/fuchsia-controller/end_to_end_tests/mobly/mobly_controller",
    ]
    libraries += target_identity_libraries
    params_source = "params_vim3.yaml"
    environments = [ vim3_env ]
    deps = target_identity_deps
  }

  python_mobly_test("nuc_target_identity_tests") {
    main_source = "target_identity_tests.py"
    libraries = [
      "//src/developer/ffx/lib/fuchsia-controller:fidl_bindings",
      "//src/developer/ffx/lib/fuchsia-controller/end_to_end_tests/mobly/mobly_controller",
    ]
    libraries += target_identity_libraries
    params_source = "params_x64.yaml"
    environments = [
      nuc7_env,
      nuc11_env,
    ]
    deps = target_identity_deps
  }

  python_mobly_test("echo_stress_test") {
    main_source = "echo_stress_test.py"
    libraries = [
      "//src/developer/ffx/lib/fuchsia-controller:fidl_bindings",
      "//src/developer/ffx/lib/fuchsia-controller/end_to_end_tests/mobly/mobly_controller",
      "//sdk/fidl/fuchsia.developer.remotecontrol:fuchsia.developer.remotecontrol_python",
    ]
    environments = [
      emu_env,
      vim3_env,
      nuc7_env,
      nuc11_env,
    ]
    deps = [
      "//src/developer/ffx/lib/fuchsia-controller/tests:core_sdk_host_test_data",
      "//src/developer/ffx/lib/fuchsia-controller/tests:fuchsia_controller_test_data",
    ]
  }

  python_mobly_test("reboot_test") {
    main_source = "reboot_test.py"
    libraries = [
      "//src/developer/ffx/lib/fuchsia-controller:fidl_bindings",
      "//src/developer/ffx/lib/fuchsia-controller/end_to_end_tests/mobly/mobly_controller",
      "//sdk/fidl/fuchsia.hardware.power.statecontrol:fuchsia.hardware.power.statecontrol_python",
      "//src/developer/ffx/fidl:fuchsia.developer.ffx_python",
    ]
    environments = [
      emu_env,
      vim3_env,
      nuc7_env,
      nuc11_env,
    ]
    deps = [
      "//src/developer/ffx/lib/fuchsia-controller/tests:core_sdk_host_test_data",
      "//src/developer/ffx/lib/fuchsia-controller/tests:fuchsia_controller_test_data",
    ]
  }

  host_test_data("tracing_runtime_deps") {
    sources = [ "$root_out_dir/trace2json" ]
    outputs = [ "$root_build_dir/tracing_runtime_deps/{{source_file_part}}" ]
    deps = [ "//src/performance/trace2json:bin" ]
  }

  python_mobly_test("tracing_test") {
    main_source = "tracing_test.py"
    libraries = [
      "//src/developer/ffx/lib/fuchsia-controller:fidl_bindings",
      "//src/developer/ffx/lib/fuchsia-controller/end_to_end_tests/mobly/mobly_controller",
      "//sdk/fidl/fuchsia.tracing:fuchsia.tracing_python",
      "//sdk/fidl/fuchsia.tracing.controller:fuchsia.tracing.controller_python",
    ]
    environments = [
      emu_env,
      vim3_env,
      nuc7_env,
      nuc11_env,
    ]
    deps = [
      ":tracing_runtime_deps",
      "//src/developer/ffx/lib/fuchsia-controller/tests:core_sdk_host_test_data",
      "//src/developer/ffx/lib/fuchsia-controller/tests:fuchsia_controller_test_data",
    ]
  }

  python_mobly_test("multi_device_test") {
    main_source = "multi_device_test.py"
    libraries = [
      "//src/developer/ffx/lib/fuchsia-controller:fidl_bindings",
      "//src/developer/ffx/lib/fuchsia-controller/end_to_end_tests/mobly/mobly_controller",
      "//sdk/fidl/fuchsia.bluetooth.sys:fuchsia.bluetooth.sys_python",
    ]
    multi_fuchsia_device = true
    deps = [
      ":tracing_runtime_deps",
      "//src/developer/ffx/lib/fuchsia-controller/tests:fuchsia_controller_test_data",
    ]
  }
}
