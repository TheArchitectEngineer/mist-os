# Copyright 2025 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/board.gni")
import("//build/rust/rustc_test.gni")
import("//src/developer/ffx/config.gni")

group("host_tests") {
  testonly = true
  deps = []

  # Also check for has_board, since this test cannot run on a configuration without a
  # board.
  if (is_host && is_linux && has_board) {
    deps += [ ":emu_e2e_host_test" ]
  }
}

# Also check for has_board, since this test cannot run on a configuration without a
# board.
if (is_host && is_linux && has_board) {
  rustc_test("emu_e2e_host_test") {
    edition = "2021"
    sources = [ "src/lib.rs" ]

    configs += [ ":test_config" ]

    deps = [
      "//src/developer/ffx/lib/executor:lib",
      "//src/developer/ffx/plugins/emulator/commands/stop/command_output",
      "//src/lib/fuchsia",
      "//third_party/rust_crates:anyhow",
      "//third_party/rust_crates:tempfile",
    ]
    data_deps = [
      "//src/developer/ffx:test_data",
      "//src/developer/ffx/lib/e2e_emu:isolated_emulator_data",
      "//src/developer/ffx/plugins/emulator:ffx_emulator_tool_test_data",
    ]
    exclude_toolchain_tags = [
      "asan",
      "hwasan",
      "tsan",
      "ubsan",
      "sancov",
      "lsan",
      "fuzzer",
      "gcc",
    ]
  }

  ffx = "${ffx_test_host_tools_out_dir}/ffx"
  config("test_config") {
    rustenv = [ "FFX_PATH=" + rebase_path(ffx, root_build_dir) ]
  }
}
