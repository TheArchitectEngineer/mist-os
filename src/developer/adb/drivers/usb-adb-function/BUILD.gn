# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("usb_adb_bind") {
  rules = "meta/usb_adb.bind"
  bind_output = "usb-adb-function.bindbc"
  tests = "meta/bind-tests.json"
  deps = [
    "//src/devices/bind/fuchsia.google.platform.usb",
    "//src/devices/bind/fuchsia.usb",
    "//src/devices/bind/fuchsia.usb.comm",
  ]
}

source_set("usb-adb-common") {
  sources = [
    "adb-function.cc",
    "adb-function.h",
  ]
  public_deps = [
    ":usb_adb_bind",
    "//sdk/banjo/fuchsia.hardware.usb:fuchsia.hardware.usb_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.usb.function:fuchsia.hardware.usb.function_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.usb.request:fuchsia.hardware.usb.request_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.adb:fuchsia.hardware.adb_cpp",
    "//sdk/fidl/fuchsia.hardware.usb.function:fuchsia.hardware.usb.function_cpp",
    "//sdk/lib/async-loop:async-loop-cpp",
    "//sdk/lib/driver/compat/cpp",
    "//sdk/lib/driver/component/cpp",
    "//sdk/lib/driver/devfs/cpp",
    "//src/devices/usb/lib/usb:peripheral",
    "//src/devices/usb/lib/usb:usb-dfv2-compat-logging",
    "//src/devices/usb/lib/usb-endpoint:usb-endpoint-client-dfv2-compat-logging",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/sync:sync-cpp",
    "//zircon/system/ulib/trace",
  ]
}

fuchsia_cc_driver("usb-adb-function-driver") {
  output_name = "usb-adb-function"
  deps = [ ":usb-adb-common" ]
}

fuchsia_driver_component("usb-adb-function") {
  component_name = "usb-adb-function"
  deps = [ ":usb-adb-function-driver" ]
  info = "meta/usb-adb-function-info.json"
  manifest = "meta/usb-adb-function.cml"
}

fuchsia_driver_package("package") {
  package_name = "usb-adb-function"
  driver_components = [ ":usb-adb-function" ]
}

test("usb-adb-test") {
  sources = [ "adb-function-test.cc" ]
  deps = [
    ":usb-adb-common",
    "//sdk/banjo/fuchsia.hardware.usb.function:fuchsia.hardware.usb.function_banjo_cpp_mock",
    "//sdk/fidl/fuchsia.hardware.adb:fuchsia.hardware.adb_cpp",
    "//sdk/lib/async-default",
    "//sdk/lib/async-loop:async-loop-cpp",
    "//sdk/lib/async-loop:async-loop-default",
    "//sdk/lib/driver/testing/cpp",
    "//src/devices/usb/lib/usb:usb-dfv2-compat-logging",
    "//src/devices/usb/lib/usb-endpoint:fake-usb-endpoint-server",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("usb-adb-unittest") {
  deps = [ ":usb-adb-test" ]
}

group("tests") {
  testonly = true
  deps = [
    ":usb-adb-unittest",
    ":usb_adb_bind_test",
  ]
}
