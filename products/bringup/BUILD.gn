# Copyright 2024 The Fuchsia Authors.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/assembly/bootfs_files_for_assembly.gni")
import("//build/assembly/product_assembly_configuration.gni")

# This is the base platform configuration used by bringup
_platform_configuration_base = {
  feature_set_level = "bootstrap"
  build_type = "eng"
  kernel = {
    oom_behavior = "job_kill"
  }
  development_support = {
    include_netsvc = true
  }
  storage = {
    filesystems = {
      image_name = "bringup"
      image_mode = "no_image"
    }
  }
}

product_assembly_configuration("bringup") {
  testonly = true
  platform = {
    forward_variables_from(_platform_configuration_base, "*")
  }
}

product_assembly_configuration("bringup_with_tests") {
  testonly = true
  platform = {
    forward_variables_from(_platform_configuration_base, "*")
    development_support.include_bootstrap_testing_framework = true
    power = {
      enable_non_hermetic_testing = true
    }
  }

  bootfs_packages = [
    "//src/power/testing/system-integration/example:bootstrap_pkg",
    "//src/power/testing/system-integration/storage:bootstrap_pkg",
  ]

  bootfs_files_labels = [ ":bootfs_test_files" ]
}

bootfs_files_for_assembly("bootfs_test_files") {
  testonly = true
  deps = [
    # This leads to all the individual bootfs_test() targets.
    "//src/bringup/tests:bootfs-tests",

    # Add the testrunner.
    "//src/testing/runtests",
  ]
}
