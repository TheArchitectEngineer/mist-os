# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/go/go_binary.gni")
import("//build/go/go_library.gni")
import("//build/go/go_test.gni")

go_library("lib") {
  sources = [
    "fssh/fssh.go",
    "fssh/synckeys.go",
    "fssh/tunnel.go",
  ]
  deps = [
    ":synckeys",
    ":tunnel",
    "//third_party/golibs:github.com/google/subcommands",
    "//tools/lib/color",
    "//tools/lib/logger",
    "//tools/sdk-tools/sdkcommon",
  ]
}

go_test("fssh-test") {
  library = ":lib"
  sources = [
    "fssh/fssh_test.go",
    "fssh/tunnel_test.go",
  ]
}

go_binary("fssh") {
  sources = [ "cmd/main.go" ]
  deps = [ ":lib" ]
}

install_host_tools("fssh-host") {
  deps = [
    ":fssh",
    "//tools/sdk-tools/sdkcommon:host-tool-deps",
  ]
  outputs = [ "fssh" ]
}

go_library("synckeys") {
  source_dir = "synckeys"
  deps = [
    "//third_party/golibs:github.com/google/go-cmp",
    "//tools/lib/logger",
  ]
  sources = [
    "synckeys.go",
    "synckeys_test.go",
  ]
}

go_test("synckeys-tests") {
  library = ":synckeys"
}

go_library("tunnel") {
  source_dir = "tunnel"
  deps = [ "//tools/lib/logger" ]
  sources = [
    "tunnel.go",
    "tunnel_test.go",
  ]
}

go_test("tunnel-tests") {
  library = ":tunnel"
}

sdk_host_tool("fssh_sdk") {
  category = "partner"
  output_name = "fssh"
  deps = [ ":fssh" ]
}

group("tests") {
  testonly = true
  deps = [
    ":fssh-test($host_toolchain)",
    ":synckeys-tests($host_toolchain)",
    ":tunnel-tests($host_toolchain)",
  ]
}
