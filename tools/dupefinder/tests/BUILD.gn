# Copyright 2025 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/compiled_action.gni")
import("//build/rust/rustc_binary.gni")
import("//build/testing/golden_files.gni")

group("host_tests") {
  testonly = true
  deps = [
    ":dupefinder_html_goldens",
    "e2e:host_tests",
  ]
}

_golden_tests = [ "from_e2e_test" ]

_comparisons = []
_goldens_deps = []
foreach(test_name, _golden_tests) {
  _json_path = "goldens/$test_name.json"
  _golden_path = "goldens/$test_name.golden.html"
  _candidate_path = "${target_out_dir}/${test_name}.html"

  _render_label = test_name + ".render"
  compiled_action(_render_label) {
    testonly = true
    tool = ":render_html_for_goldens"
    inputs = [ _json_path ]
    outputs = [ _candidate_path ]
    args = [
      "--json-dump",
      rebase_path(_json_path, root_build_dir),
      "--html-output",
      rebase_path(_candidate_path, root_build_dir),
    ]
  }

  _goldens_deps += [ ":${_render_label}" ]

  _comparisons += [
    {
      golden = _golden_path
      candidate = _candidate_path
    },
  ]
}

golden_files("dupefinder_html_goldens") {
  testonly = true
  comparisons = _comparisons
  deps = _goldens_deps
}

rustc_binary("render_html_for_goldens") {
  testonly = true
  edition = "2021"
  source_root = "render_html_for_goldens.rs"
  sources = [ source_root ]
  deps = [
    "//src/lib/fuchsia",
    "//third_party/rust_crates:argh",
    "//third_party/rust_crates:camino",
    "//third_party/rust_crates:serde_json",
    "//tools/dupefinder:lib",
  ]
}
