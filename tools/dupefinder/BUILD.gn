# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/host.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_library.gni")

if (is_host) {
  rustc_binary("bin") {
    name = "dupefinder"
    edition = "2021"
    sources = [ "src/main.rs" ]
    deps = [
      ":lib",
      "//src/lib/fuchsia",
      "//third_party/rust_crates:anyhow",
      "//third_party/rust_crates:argh",
      "//third_party/rust_crates:camino",
      "//third_party/rust_crates:chrono",
      "//third_party/rust_crates:log",
      "//third_party/rust_crates:serde_json",
    ]
  }

  rustc_library("lib") {
    name = "dupefinder"
    edition = "2021"
    sources = [ "src/lib.rs" ]
    deps = [
      "//src/performance/memory/pprof:pprof_proto",
      "//third_party/rust_crates:handlebars",
      "//third_party/rust_crates:log",
      "//third_party/rust_crates:percent-encoding",
      "//third_party/rust_crates:prost",
      "//third_party/rust_crates:serde",
      "//third_party/rust_crates:serde_json",
      "//third_party/rust_crates:thiserror",
    ]
    inputs = [ "templates/report.html.hbs" ]

    # The public API of this library is only exposed for internal testing.
    visibility = [
      ":*",
      "tests:*",
      "tests/e2e:*",
    ]
  }

  install_host_tools("dupefinder") {
    deps = [ ":bin" ]
    outputs = [ "dupefinder" ]
  }

  group("host_tests") {
    testonly = true
    deps = [ "tests:host_tests" ]
  }
}
