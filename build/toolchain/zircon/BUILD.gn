# Copyright 2024 Mist Tecnologia LTDA. All rights reserved.
# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/zircon/standard.gni")
import("//build/toolchain/zircon/zircon_toolchain_suite.gni")

if (current_toolchain == default_toolchain) {
  foreach(cpu, standard_fuchsia_cpus) {
    zircon_toolchain_suite("user.basic_$cpu") {
      cpu = cpu
      if (is_mistos) {
        os = "mistos"
      } else {
        os = "fuchsia"
      }
      strip = "--strip-sections"
      environment = "user.basic"

      configs = [
        "//build/config/zircon:user",
        "//build/config/zircon:user-link",
        "//build/config:no-compiler-abi",
      ]
      source_deps = [ "//build/config/zircon:user_deps" ]

      executable_configs = [ "//build/config/zircon:user-executable" ]

      tags = [
        "no-compiler-abi",
        "no-floating-point",
        "standalone",
      ]
      exclude_variant_tags = [ "needs-compiler-abi" ]
    }
  }
}
