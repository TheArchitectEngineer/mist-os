# Copyright 2025 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bazel/bazel_build_action.gni")

# A GN wrapper for a Bazel product configuration target. This template is useful
# for exporting a bazel product config back to GN land to be discovered in the
# products.json build api.
#
# Parameters
#
#  name (optional)
#    Name of the product.
#    Type: string
#    Default: $target_name
#
#  bazel_product_configuration_target (required)
#    The Bazel product configuration target to build.
#    Type: label (from BUILD.bazel)
#
#  deps
#  metadata
#  testonly
#  visibility
#
template("bazel_product_configuration") {
  assert(defined(invoker.bazel_product_configuration_target),
         "bazel_product_configuration_target is required")

  product_name = target_name
  if (defined(invoker.name)) {
    product_name = invoker.name
  }

  product_configuration_target = target_name
  product_configuration_out =
      "${target_out_dir}/${product_configuration_target}"
  bazel_target = invoker.bazel_product_configuration_target

  bazel_build_action(product_configuration_target) {
    forward_variables_from(invoker,
                           [
                             "deps",
                             "metadata",
                             "testonly",
                             "visibility",
                             "inputs",
                             "remote_build",
                           ])

    bazel_target = bazel_target

    # Directory outputs are OK because `product_configuration.json` correctly
    # represents the freshness of all outputs.
    directory_outputs = [
      {
        bazel_dir = "{{BAZEL_TARGET_OUT_DIR}}/{{BAZEL_TARGET_NAME}}"
        ninja_dir = product_configuration_target
        tracked_files = [ "product_configuration.json" ]
      },
    ]

    metadata = {
      product_configs = [
        {
          label = get_label_info(":${product_configuration_target}",
                                 "label_with_toolchain")
          name = product_name
          outdir = rebase_path(product_configuration_out, root_build_dir)
        },
      ]
    }
  }
}
