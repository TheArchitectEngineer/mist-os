# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/host.gni")
import("//build/rust/rustc_binary.gni")

if (is_host) {
  executable("json_validator") {
    sources = [ "main.cc" ]

    deps = [
      "//src/lib/fxl:cli_no_logging",
      "//third_party/rapidjson",
      "//third_party/re2",
    ]
  }

  rustc_binary("json_validator_valico") {
    edition = "2021"
    sources = [ "src/main.rs" ]

    deps = [
      "//third_party/rust_crates:argh",
      "//third_party/rust_crates:serde_json",
      "//third_party/rust_crates:serde_json5",
      "//third_party/rust_crates:valico",
    ]
  }
}

install_host_tools("install") {
  deps = [
    ":json_validator",
    ":json_validator_valico",
  ]

  outputs = [
    "json_validator",
    "json_validator_valico",
  ]
}

group("tests") {
  testonly = true
  deps = [ "test:tests" ]
}

if (current_toolchain == default_toolchain) {
  import("//build/bazel/bazel_inputs.gni")

  # Creates "@gn_targets//build/tools/json_validator:json_validator_valico".
  bazel_input_file("json_validator_valico.bazel_input") {
    generator = ":json_validator_valico($host_toolchain)"

    # LINT.IfChange
    outputs =
        [ get_label_info(generator, "root_out_dir") + "/json_validator_valico" ]

    # LINT.ThenChange(//build/bazel/bazel_sdk/rules.bzl)
  }
}
