# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")

group("test") {
  testonly = true
  deps = [ ":hash-table-test" ]
}

test("hash-table-test") {
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  visibility = [ ":*" ]
  output_name = "trace-engine-hash-table-test"
  sources = [
    "../hash_table.h",
    "hash_table_test.cc",
  ]
  deps = [
    "//sdk/lib/fdio",
    "//zircon/system/ulib/zxtest",
  ]
}

test("buffer_test") {
  visibility = [ ":*" ]
  output_name = "buffer_test"
  sources = [
    "../buffer.cc",
    "../buffer.h",
    "../rolling_buffer.cc",
    "../rolling_buffer.h",
    "buffer_test.cc",
    "rolling_buffer_test.cc",
  ]
  deps = [
    "//src/lib/fxl/test:gtest_main",
    "//zircon/system/ulib/trace-reader",
  ]
}

test("trace_engine_test") {
  visibility = [ ":*" ]
  sources = [
    "trace_engine_test.cc",
    "trace_engine_writing_test.cc",
  ]
  deps = [
    "//sdk/lib/async-loop:async-loop-cpp",
    "//sdk/lib/async-loop:async-loop-default",
    "//sdk/lib/fit",
    "//src/lib/fxl/test:gtest_main",
    "//zircon/system/ulib/trace-engine",
    "//zircon/system/ulib/trace-reader",
  ]
}

fuchsia_unittest_package("hash-table-test-pkg") {
  package_name = "trace-engine-hash-table-test"
  deps = [ ":hash-table-test" ]
}

fuchsia_unittest_package("trace-engine-test-pkg") {
  package_name = "trace-engine-test"
  deps = [ ":trace_engine_test" ]
}

fuchsia_unittest_package("buffer-test-pkg") {
  package_name = "buffer-test"
  deps = [ ":buffer_test" ]
}

group("tests") {
  testonly = true
  deps = [
    ":buffer-test-pkg",
    ":hash-table-test-pkg",
    ":trace-engine-test-pkg",
  ]
}
