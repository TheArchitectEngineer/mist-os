# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/cpp/library_headers.gni")

source_set("vmo") {
  testonly = true
  sources = [
    "vmo-clone.cc",

    # TODO(stevensd): Rename once the clone flag names are cleaned up
    "vmo-clone2.cc",
    "vmo-reference.cc",
    "vmo-signal.cc",
    "vmo-slice.cc",
    "vmo-transfer-data.cc",
    "vmo-zero.cc",
    "vmo.cc",
  ]
  deps = [
    ":helpers",
    "//sdk/lib/fit",
    "//src/zircon/testing/maybe-standalone-test",
    "//zircon/kernel/lib/arch",
    "//zircon/system/ulib/explicit-memory",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",
    "//zircon/system/utest/core/pager:userpager",
  ]

  defines = []
  if (is_asan) {
    # Due to how the kernel manages the PMM under ASAN memory can become more fragmented resulting
    # in less chance of succeeding large contiguous allocation requests.
    defines += [ "LIMIT_CONTIGUOUS_ALLOCATIONS=1" ]
  } else {
    defines += [ "LIMIT_CONTIGUOUS_ALLOCATIONS=0" ]
  }
}

library_headers("helpers_headers") {
  include_dir = "."
  headers = [ "helpers.h" ]
}

source_set("helpers") {
  testonly = true
  sources = [ "helpers.cc" ]
  public_deps = [ ":helpers_headers" ]
  deps = [
    "//sdk/lib/fit",
    "//src/zircon/testing/maybe-standalone-test",
    "//zircon/kernel/lib/boot-options",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",
  ]
}
