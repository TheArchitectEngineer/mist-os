# Copyright 2025 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/devicetree/devicetree.gni")
import("//build/devicetree/devicetree_visitor.gni")
import("//build/test.gni")

group("tests") {
  deps = [ ":interconnect-visitor-test" ]
  testonly = true
}

devicetree_visitor("interconnect-visitor") {
  deps = [ ":interconnect" ]
}

source_set("interconnect") {
  sources = [
    "interconnect-visitor.cc",
    "interconnect-visitor.h",
  ]
  deps = [
    "//sdk/fidl/fuchsia.hardware.interconnect:fuchsia.hardware.interconnect_bindlib_cpp",
    "//sdk/lib/driver/component/cpp:cpp",
    "//sdk/lib/driver/devicetree/manager",
    "//sdk/lib/driver/devicetree/visitors:common-types",
    "//sdk/lib/driver/devicetree/visitors:registration",
    "//sdk/lib/driver/logging/cpp",
    "//src/devices/bind/fuchsia:fuchsia_cpp",
    "//src/devices/bind/fuchsia.interconnect:fuchsia.interconnect_cpp",
    "//src/lib/ddk",
  ]
  public_deps = [
    "//sdk/fidl/fuchsia.hardware.interconnect:fuchsia.hardware.interconnect_cpp",
    "//sdk/lib/driver/devicetree/visitors:driver-visitor",
  ]
}

test("interconnect-visitor-test-bin") {
  sources = [ "test/interconnect-visitor-test.cc" ]
  deps = [
    ":interconnect",
    "//sdk/fidl/fuchsia.hardware.interconnect:fuchsia.hardware.interconnect_bindlib_cpp",
    "//sdk/lib/driver/devicetree/testing:visitor-test-helper",
    "//sdk/lib/driver/devicetree/visitors:registry",
    "//sdk/lib/driver/devicetree/visitors/default/bind-property",
    "//sdk/lib/driver/devicetree/visitors/default/mmio",
    "//src/devices/bind/fuchsia:fuchsia_cpp",
    "//src/devices/bind/fuchsia.interconnect:fuchsia.interconnect_cpp",
    "//src/devices/bind/fuchsia.platform:fuchsia.platform_cpp",
    "//src/lib/fxl/test:gtest_main",
  ]
}

dtb("interconnect-dtb") {
  sources = [ "test/dts/interconnect.dts.S" ]
}

resource("interconnect-dtb-file") {
  sources = get_target_outputs(":interconnect-dtb")
  outputs = [ "test-data/{{source_file_part}}" ]
  deps = [ ":interconnect-dtb" ]
}

dtb("interconnect-incorrect-cell-count-dtb") {
  sources = [ "test/dts/interconnect-incorrect-cell-count.dts.S" ]
}

resource("interconnect-incorrect-cell-count-dtb-file") {
  sources = get_target_outputs(":interconnect-incorrect-cell-count-dtb")
  outputs = [ "test-data/{{source_file_part}}" ]
  deps = [ ":interconnect-incorrect-cell-count-dtb" ]
}

fuchsia_unittest_package("interconnect-visitor-test") {
  component_name = "interconnect-visitor-test"
  deps = [
    ":interconnect-dtb-file",
    ":interconnect-incorrect-cell-count-dtb-file",
    ":interconnect-visitor-test-bin",
  ]

  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}
