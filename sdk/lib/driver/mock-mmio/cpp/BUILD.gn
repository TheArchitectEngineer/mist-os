# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/cpp/sdk_source_set.gni")
import("//build/test.gni")

sdk_source_set("cpp") {
  # TODO(https://fxbug.dev/382792202): Promote category to "partner".
  category = "internal"
  stable = false
  sdk_name = "mock_mmio_cpp"
  include_base = "//sdk"

  sources = [
    "mock-mmio-range.cc",
    "mock-mmio-range.h",
    "mock-mmio-reg.cc",
    "mock-mmio-reg.h",
  ]

  public_deps = [ "//src/devices/lib/mmio" ]

  public_configs = [ "//sdk/config" ]
}

group("tests") {
  testonly = true
  deps = [
    ":mock-mmio-range-test-bin",
    ":mock-mmio-reg-test-bin",
  ]
}

test("mock-mmio-reg-test-bin") {
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  output_name = "mock-mmio-reg-sdk-test"
  sources = [ "tests/mock-mmio-reg-test.cc" ]
  deps = [
    ":cpp",
    "//src/devices/lib/mmio",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gtest",
  ]
}

test("mock-mmio-range-test-bin") {
  output_name = "mock_mmio_range_cpp_test"
  sources = [ "tests/mock-mmio-range-test.cc" ]
  deps = [
    ":cpp",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gtest",
  ]
}

fuchsia_unittest_package("mock-mmio-cpp-test") {
  deps = [
    ":mock-mmio-range-test-bin",
    ":mock-mmio-reg-test-bin",
  ]
}
