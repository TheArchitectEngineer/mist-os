# Copyright 2025 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/fidl/fidl.gni")
import("//build/rust/rustc_binary.gni")

group("optimistic_lease_management") {
  deps = [ ":lease_management" ]
}

group("tests") {
  testonly = true
  deps = [ ":package" ]
}

fidl("fuchsia.example.power") {
  sources = [ "fidl/power.fidl" ]
  public_deps = [ "//zircon/vdso/zx" ]
}

fuchsia_component("test-component") {
  testonly = true
  component_name = "test"
  manifest = "meta/test.cml"

  deps = [ ":test_bin" ]
}

fuchsia_test_package("package") {
  package_name = "olm"
  test_components = [ ":test-component" ]
  deps = [
    ":client",
    ":server",
  ]
  subpackages = [ "//src/power/testing/client:power-framework" ]
}

fuchsia_component("server") {
  manifest = "meta/server.cml"
  deps = [ ":server_bin" ]
}

rustc_binary("server_bin") {
  edition = "2021"
  name = "olm_server"
  source_root = "src/server.rs"
  sources = [ "src/server.rs" ]
  deps = [
    ":fuchsia.example.power_rust",
    ":lease_management",
    "//sdk/fidl/fuchsia.power.system:fuchsia.power.system_rust",
    "//sdk/rust/zx",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/trace/rust:trace-provider",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
  ]
}

fuchsia_component("client") {
  manifest = "meta/client.cml"
  deps = [ ":client_bin" ]
}

rustc_binary("client_bin") {
  edition = "2021"
  name = "olm_client"
  source_root = "src/client.rs"
  sources = [ "src/client.rs" ]
  deps = [
    ":fuchsia.example.power_rust",
    ":lease_management",
    "//sdk/rust/zx",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/trace/rust:trace-provider",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
  ]
}

rustc_library("lease_management") {
  edition = "2021"
  deps = [
    ":fuchsia.example.power_rust",
    "//sdk/fidl/fuchsia.power.system:fuchsia.power.system_rust",
    "//sdk/rust/zx",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/trace/rust:trace",
    "//third_party/rust_crates:async-trait",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
  ]
  source_root = "lib/lib.rs"
  sources = [
    "lib/flush_trigger.rs",
    "lib/lib.rs",
    "lib/sequence_client.rs",
    "lib/sequence_server.rs",
  ]
}

rustc_test("test_bin") {
  edition = "2021"
  source_root = "src/test.rs"

  output_name = "olm_test_bin"
  sources = [ "src/test.rs" ]

  deps = [
    ":fuchsia.example.power_rust",
    "//sdk/fidl/fuchsia.hardware.suspend:fuchsia.hardware.suspend_rust",
    "//sdk/fidl/fuchsia.power.system:fuchsia.power.system_rust",
    "//sdk/fidl/fuchsia.testing.harness:fuchsia.testing.harness_rust",
    "//sdk/fidl/fuchsia.tracing.provider:fuchsia.tracing.provider_rust",
    "//sdk/rust/zx",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-component-test",
    "//src/power/system-activity-governor/fake/fidl:test.sagcontrol_rust",
    "//src/power/system-activity-governor/integration/testing/fidl:test.systemactivitygovernor_rust",
    "//src/power/testing/client/rust",
    "//src/power/testing/fake-suspend:test.suspendcontrol_rust",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
    "//third_party/rust_crates:rand",
  ]
}
